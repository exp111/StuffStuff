<html lang="en">
    <head>
        <meta charset="utf-8">
        <title>Genetics</title>
        <script>
            //https://raw.githubusercontent.com/goonstation/goonstation/master/code/modules/medical/genetics/combo_recipes.dm
            const recipeCombinationsRaw = `/datum/geneticsrecipe
	var/list/required_effects = list()
	var/result = null

// Beneficial

/datum/geneticsrecipe/breathless
	required_effects = list("adrenaline","ithillid")
	result = /datum/bioEffect/breathless

/datum/geneticsrecipe/hulk // Discovered
	required_effects = list("strong","radioactive")
	result = /datum/bioEffect/hulk

/datum/geneticsrecipe/xray // Discovered
	required_effects = list("eyebeams","blind")
	result = /datum/bioEffect/xray

/datum/geneticsrecipe/regenerator // Discovered
	required_effects = list("adrenaline","healing_touch")
	result = /datum/bioEffect/regenerator

/datum/geneticsrecipe/toxic_farts
	required_effects = list("farty","stinky")
	result = /datum/bioEffect/toxic_farts

/datum/geneticsrecipe/thermal_res
	required_effects = list("fire_resist","cold_resist")
	result = /datum/bioEffect/thermalres

/datum/geneticsrecipe/fire_resist // Discovered
	required_effects = list("immolate","glowy")
	result = /datum/bioEffect/fireres

/datum/geneticsrecipe/cold_resist // Discovered
	required_effects = list("cryokinesis","glowy")
	result = /datum/bioEffect/coldres

/datum/geneticsrecipe/rad_resist // Discovered
	required_effects = list("radioactive","glowy")
	result = /datum/bioEffect/rad_resist

/datum/geneticsrecipe/alch_resist
	required_effects = list("drunk","detox")
	result = /datum/bioEffect/alcres

/datum/geneticsrecipe/radio_brain
	required_effects = list("psy_resist","loud_voice")
	result = /datum/bioEffect/radio_brain
	
/datum/geneticsrecipe/blood_overdrive
	required_effects = list("anemia","polycythemia")
	result = /datum/bioEffect/blood_overdrive

// Detrimental

/datum/geneticsrecipe/unintelligable // Discovered
	required_effects = list("loud_voice","quiet_voice")
	result = /datum/bioEffect/speech/unintelligable

/datum/geneticsrecipe/unintelligable_two
	required_effects = list("accent_swedish","accent_elvis")
	result = /datum/bioEffect/speech/unintelligable

/datum/geneticsrecipe/vowels
	required_effects = list("accent_swedish","accent_chav")
	result = /datum/bioEffect/speech/vowelitis

/datum/geneticsrecipe/coprolalia
	required_effects = list("accent_chav","accent_tommy")
	result = /datum/bioEffect/coprolalia

/datum/geneticsrecipe/epilepsy
	required_effects = list("bad_eyesight","flashy")
	result = /datum/bioEffect/epilepsy

/datum/geneticsrecipe/blind
	required_effects = list("bad_eyesight","narcolepsy")
	result = /datum/bioEffect/blind

/datum/geneticsrecipe/mute // Discovered
	required_effects = list("quiet_voice","screamer")
	result = /datum/bioEffect/mute

/datum/geneticsrecipe/drunk // Discovered
	required_effects = list("detox","stinky")
	result = /datum/bioEffect/drunk

/datum/geneticsrecipe/radioactive
	required_effects = list("aura","stinky")
	result = /datum/bioEffect/radioactive

/datum/geneticsrecipe/mutagenic_field
	required_effects = list("radioactive","involuntary_teleporting")
	result = /datum/bioEffect/mutagenic_field

/datum/geneticsrecipe/tourettes
	required_effects = list("clumsy","coprolalia")
	result = /datum/bioEffect/tourettes
	
/datum/geneticsrecipe/buzz
	required_effects = list("bee","stinky")
	result = /datum/bioEffect/buzz
	
// Useless

/datum/geneticsrecipe/glowy_one
	required_effects = list("shiny","albinism")
	result = /datum/bioEffect/glowy

/datum/geneticsrecipe/glowy_two
	required_effects = list("shiny","melanism")
	result = /datum/bioEffect/glowy

/datum/geneticsrecipe/glowy_three
	required_effects = list("aura","shiny")
	result = /datum/bioEffect/glowy

/datum/geneticsrecipe/shiny
	required_effects = list("glowy","aura")
	result = /datum/bioEffect/particles

/datum/geneticsrecipe/aura
	required_effects = list("glowy","shiny")
	result = /datum/bioEffect/aura

/datum/geneticsrecipe/fire_aura_one
	required_effects = list("aura","immolate")
	result = /datum/bioEffect/fire_aura

/datum/geneticsrecipe/fire_aura_two
	required_effects = list("aura","fire_breath")
	result = /datum/bioEffect/fire_aura

/datum/geneticsrecipe/strong
	required_effects = list("fat","detox")
	result = /datum/bioEffect/strong

/datum/geneticsrecipe/stinky
	required_effects = list("farty","dead_scan")
	result = /datum/bioEffect/stinky

/datum/geneticsrecipe/bee
	required_effects = list("roach","detox")
	result = /datum/bioEffect/bee

// Powers

/datum/geneticsrecipe/telekinesis // Discovered
	required_effects = list("telepathy","radio_brain")
	result = /datum/bioEffect/power/telekinesis_drag

/datum/geneticsrecipe/eyebeams // Discovered
	required_effects = list("bad_eyesight","glowy")
	result = /datum/bioEffect/power/eyebeams

/datum/geneticsrecipe/superfart // Discovered
	required_effects = list("loud_voice","farty")
	result = /datum/bioEffect/power/superfart

/datum/geneticsrecipe/cryokinesis
	required_effects = list("chime_snaps","fire_resist")
	result = /datum/bioEffect/power

/datum/geneticsrecipe/adrenaline
	required_effects = list("detox","strong")
	result = /datum/bioEffect/power/adrenaline

/datum/geneticsrecipe/jumpy
	required_effects = list("strong","monkey")
	result = /datum/bioEffect/power/jumpy

/datum/geneticsrecipe/telepath
	required_effects = list("psy_resist","quiet_voice")
	result = /datum/bioEffect/power/telepathy

/datum/geneticsrecipe/midas
	required_effects = list("chime_snaps","drunk")
	result = /datum/bioEffect/power/midas

/datum/geneticsrecipe/midas_two // Discovered
	required_effects = list("chime_snaps","shiny")
	result = /datum/bioEffect/power/midas

/datum/geneticsrecipe/healing_touch // Discovered
	required_effects = list("midas","detox")
	result = /datum/bioEffect/power/healing_touch
	
/datum/geneticsrecipe/healing_touch_two 
	required_effects = list("midas","melt")
	result = /datum/bioEffect/power/healing_touch

/datum/geneticsrecipe/dimension_shift // Discovered
	required_effects = list("radio_brain","involuntary_teleporting")
	result = /datum/bioEffect/power/dimension_shift

/datum/geneticsrecipe/fire_breath
	required_effects = list("cough","immolate")
	result = /datum/bioEffect/power/fire_breath

/datum/geneticsrecipe/bigpuke
	required_effects = list("cough","drunk")
	result = /datum/bioEffect/power/bigpuke

/datum/geneticsrecipe/bigpuke_two
	required_effects = list("cough","stinky")
	result = /datum/bioEffect/power/bigpuke

/datum/geneticsrecipe/bigpuke_three
	required_effects = list("sneeze","drunk")
	result = /datum/bioEffect/power/bigpuke

/datum/geneticsrecipe/bigpuke_four
	required_effects = list("sneeze","stinky")
	result = /datum/bioEffect/power/bigpuke

/datum/geneticsrecipe/ink_one
	required_effects = list("ithillid","melanism")
	result = /datum/bioEffect/power/ink

/datum/geneticsrecipe/ink_two
	required_effects = list("ithillid","shiny")
	result = /datum/bioEffect/power/ink

/datum/geneticsrecipe/ink_three
	required_effects = list("ithillid","sneeze")
	result = /datum/bioEffect/power/ink

/datum/geneticsrecipe/photokinesis // Discovered
	required_effects = list("glowy","psy_resist")
	result = /datum/bioEffect/power/photokinesis

/datum/geneticsrecipe/photokinesis_two
	required_effects = list("shiny","psy_resist")
	result = /datum/bioEffect/power/photokinesis

/datum/geneticsrecipe/photokinesis_three
	required_effects = list("aura","psy_resist")
	result = /datum/bioEffect/power/photokinesis

/datum/geneticsrecipe/erebokinesis_one
	required_effects = list("cloak_of_darkness","psy_resist")
	result = /datum/bioEffect/power/erebokinesis

/datum/geneticsrecipe/erebokinesis_two
	required_effects = list("chameleon","psy_resist")
	result = /datum/bioEffect/power/erebokinesis

/datum/geneticsrecipe/erebokinesis_three
	required_effects = list("uncontrollable_cloak","psy_resist")
	result = /datum/bioEffect/power/erebokinesis

/datum/geneticsrecipe/brown_note
	required_effects = list("farty","loud_voice")
	result = /datum/bioEffect/power/brown_note

/datum/geneticsrecipe/brown_note_two
	required_effects = list("stinky","loud_voice")
	result = /datum/bioEffect/power/brown_note

/datum/geneticsrecipe/cloak_of_darkness // Discovered
	required_effects = list("uncontrollable_cloak","melanism")
	result = /datum/bioEffect/power/darkcloak

/datum/geneticsrecipe/chameleon // Discovered
	required_effects = list("uncontrollable_cloak","albinism")
	result = /datum/bioEffect/power/chameleon

/datum/geneticsrecipe/chameleon_two
	required_effects = list("uncontrollable_cloak","examine_stopper")
	result = /datum/bioEffect/power/chameleon

/datum/geneticsrecipe/shoot_limb
	required_effects = list("farty","bigpuke")
	result = /datum/bioEffect/power/shoot_limb

// Mutantraces

/datum/geneticsrecipe/seenoevil
	required_effects = list("blind","deaf","mute")
	result = /datum/bioEffect/mutantrace/monkey
	// since the station starts with monkey already researched we dont really need multiple recipes
	// this one's just for comedy's sake =v

/datum/geneticsrecipe/squid // Discovered
	required_effects = list("fat","stinky")
	result = /datum/bioEffect/mutantrace/ithillid

/datum/geneticsrecipe/squid_two
	required_effects = list("fat","melt")
	result = /datum/bioEffect/mutantrace/ithillid

/datum/geneticsrecipe/roach // Discovered
	required_effects = list("stinky","bee")
	result = /datum/bioEffect/mutantrace/roach

/datum/geneticsrecipe/roach_two
	required_effects = list("radioactive","bee")
	result = /datum/bioEffect/mutantrace/roach

/datum/geneticsrecipe/flashy
	required_effects = list("glowy","radioactive")
	result = /datum/bioEffect/mutantrace/flashy

/datum/geneticsrecipe/flashy_two
	required_effects = list("glowy","chameleon")
	result = /datum/bioEffect/mutantrace/flashy

/datum/geneticsrecipe/flashy_three // Discovered
	required_effects = list("glowy","epilepsy")
	result = /datum/bioEffect/mutantrace/flashy

/datum/geneticsrecipe/lizard
	required_effects = list("horns","chameleon")
	result = /datum/bioEffect/mutantrace

/datum/geneticsrecipe/lizard_two
	required_effects = list("horns","fire_resist")
	result = /datum/bioEffect/mutantrace

/datum/geneticsrecipe/dwarf // Discovered
	required_effects = list("strong","resist_alcohol")
	result = /datum/bioEffect/mutantrace/dwarf

/datum/geneticsrecipe/dwarf_two
	required_effects = list("strong","drunk")
	result = /datum/bioEffect/mutantrace/dwarf

/datum/geneticsrecipe/dwarf_three // Discovered
	required_effects = list("strong","fat")
	result = /datum/bioEffect/mutantrace/dwarf

/datum/geneticsrecipe/blank // Discovered
	required_effects = list("albinism","melanism")
	result = /datum/bioEffect/mutantrace/blank

/datum/geneticsrecipe/skeleton // Discovered
	required_effects = list("screamer","dead_scan")
	result = /datum/bioEffect/mutantrace/skeleton

/datum/geneticsrecipe/skeleton_two
	required_effects = list("cloak_of_darkness","dead_scan")
	result = /datum/bioEffect/mutantrace/skeleton

/datum/geneticsrecipe/skeleton_three
	required_effects = list("xray","dead_scan")
	result = /datum/bioEffect/mutantrace/skeleton
`
        </script>
        <script>
            `/datum/bioEffect/activator
	name = "Booster Gene X"
	desc = "This function of this gene is not well-researched."
	researched_desc = "This gene will activate every latent mutation in the subject when activated."
	id = "activator"
	secret = 1
	isBad = 1
	probability = 33
	blockCount = 2
	blockGaps = 4
	lockProb = 100
	lockedGaps = 1
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	curable_by_mutadone = 0

	New(var/for_global_list = 0)
		..()
		if (!for_global_list)
			name = "Booster Gene"

	OnAdd()
		var/mob/living/L = owner
		var/datum/bioHolder/B = L.bioHolder

		for(var/ID in B.effectPool)
			B.ActivatePoolEffect(B.effectPool[ID], 1, 0)
			//Overrides incomplete DNA sequences
		return

/datum/bioEffect/scrambler
	name = "Booster Gene Y"
	desc = "This function of this gene is not well-researched."
	researched_desc = "This gene will completely randomise the subject's gene pool and remove all active effects."
	id = "gene_scrambler"
	secret = 1
	isBad = 1
	probability = 33
	blockCount = 2
	blockGaps = 4
	lockProb = 100
	lockedGaps = 1
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	curable_by_mutadone = 0

	New(var/for_global_list = 0)
		..()
		if (!for_global_list)
			name = "Booster Gene"

	OnAdd()
		var/mob/living/L = owner
		var/datum/bioHolder/B = L.bioHolder

		B.RemoveAllEffects()
		B.BuildEffectPool()
		return

/datum/bioEffect/remove_all
	name = "Booster Gene Z"
	desc = "This function of this gene is not well-researched."
	researched_desc = "This gene will remove all active and latent effects from the subject."
	id = "gene_clearer"
	secret = 1
	isBad = 1
	probability = 33
	blockCount = 2
	blockGaps = 4
	lockProb = 100
	lockedGaps = 1
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	curable_by_mutadone = 0

	New(var/for_global_list = 0)
		..()
		if (!for_global_list)
			name = "Booster Gene"

	OnAdd()
		var/mob/living/L = owner
		var/datum/bioHolder/B = L.bioHolder

		B.RemoveAllEffects()
		B.RemoveAllPoolEffects()
		return

/datum/bioEffect/early_secret_access
	name = "High Complexity DNA"
	desc = "No effect on subject. Unlocks new research possibilities and can be used as a wildcard in combinations."
	id = "early_secret_access"
	secret = 1
	effectType = effectTypePower
	mob_exclusive = /mob/living/carbon/human/
	can_research = 0
	blockCount = 1
	probability = 35
	reclaim_fail = 100
	lockProb = 100
	blockGaps = 0
	lockedGaps = 2
	lockedDiff = 6
	lockedChars = list("G","C","A","T","U")
	lockedTries = 12
	wildcard = 1


// Largely for admin gimmicks with adding bioeffects because there's no way to make them not eat stability.
// Basically give this and then give all your fucked up superpowers or whatever
/datum/bioEffect/stability_maximizer
	name = "Genetic Stabilizer"
	desc = "Boosts the genetic stability of the target to impossibly safe levels."
	id = "stability_maximizer"
	msgGain = "Your genes feel like they're rock solid."
	msgLose = "You feel vulnerable to genetic stability again."
	probability = 0
	occur_in_genepools = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	stability_loss = -999999

///////////////////////
// Totally Crippling //
///////////////////////

/datum/bioEffect/blind
	name = "Blindness"
	desc = "Disconnects the optic nerves from the brain, rendering the subject unable to see."
	id = "blind"
	effectType = effectTypeDisability
	isBad = 1
	probability = 33
	msgGain = "You can't seem to see anything!"
	msgLose = "Your vision returns!"
	reclaim_fail = 15
	stability_loss = -20
	lockProb = 50
	lockedGaps = 2
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	icon_state  = "bad"


/datum/bioEffect/mute
	name = "Frontal Gyrus Suspension"
	desc = "Completely shuts down the speech center of the subject's brain."
	id = "mute"
	effectType = effectTypeDisability
	isBad = 1
	probability = 33
	msgGain = "You feel unable to express yourself at all."
	msgLose = "You feel able to speak freely again."
	reclaim_fail = 15
	stability_loss = -20
	lockProb = 50
	lockedGaps = 2
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	icon_state  = "bad"

/datum/bioEffect/deaf
	name = "Deafness"
	desc = "Diminishes the subject's tympanic membrane, rendering them unable to hear."
	id = "deaf"
	effectType = effectTypeDisability
	isBad = 1
	probability = 33
	blockCount = 4
	msgGain = "It's quiet. Too quiet."
	msgLose = "You can hear again!"
	reclaim_fail = 15
	stability_loss = -20
	lockProb = 50
	lockedGaps = 2
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	icon_state  = "bad"

	OnAdd()
		..()
		owner.ear_disability = 1

	OnRemove()
		..()
		owner.ear_disability = 0


///////////////////////////
// Bad but not crippling //
///////////////////////////

/datum/bioEffect/clumsy
	name = "Dyspraxia"
	desc = "Hinders transmissions in the subject's nervous system, causing poor motor skills."
	id = "clumsy"
	effectType = effectTypeDisability
	probability = 66
	isBad = 1
	msgGain = "You feel kind of off-balance and disoriented."
	msgLose = "You feel well co-ordinated again."
	reclaim_fail = 15
	stability_loss = -5
	icon_state  = "bad"

/datum/bioEffect/narcolepsy
	name = "Narcolepsy"
	desc = "Alters the sleep center of the subject's brain, causing bouts of involuntary sleepiness."
	id = "narcolepsy"
	effectType = effectTypeDisability
	probability = 66
	isBad = 1
	msgGain = "You feel a bit sleepy."
	msgLose = "You feel wide awake."
	reclaim_fail = 15
	stability_loss = -5
	var/sleep_prob = 4
	icon_state  = "bad"

	OnLife()
		if(..()) return
		var/mob/living/L = owner
		if (!L)
			return
		if (prob(sleep_prob))
			L.sleeping = 1

/datum/bioEffect/narcolepsy/super
	name = "Extreme Narcolepsy"
	desc = "Like narcolepsy, but worse and incurable."
	id = "narcolepsy_super"
	msgGain = "You feel more tired than you've ever thought possible."
	msgLose = "You feel more awake than you've ever been in your whole life."
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	sleep_prob = 35
	icon_state  = "bad"

/datum/bioEffect/coprolalia
	name = "Coprolalia"
	desc = "Causes involuntary outbursts from the subject."
	id = "coprolalia"
	effectType = effectTypeDisability
	probability = 99
	isBad = 1
	msgGain = "You can't seem to shut up!"
	msgLose = "You feel more in control."
	reclaim_fail = 15
	var/talk_prob = 10
	var/list/talk_strings = list("PISS","FUCK","SHIT","DAMN","TITS","ARGH","WOOF","CRAP","BALLS")
	icon_state  = "bad"

	OnLife()
		if(..()) return
		var/mob/living/L = owner
		if (!L)
			return
		if (isdead(L))
			return
		if (prob(talk_prob))
			L.say(pick(talk_strings))

/datum/bioEffect/fat
	name = "Obesity"
	desc = "Greatly slows the subject's metabolism, enabling greater buildup of lipid tissue."
	id = "fat"
	probability = 99
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel blubbery and lethargic!"
	msgLose = "You feel fit!"
	reclaim_fail = 15
	stability_loss = -5
	icon_state  = "bad"

	OnAdd()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.set_body_icon_dirty()
			H.unlock_medal("Space Ham", 1)
			APPLY_MOVEMENT_MODIFIER(H, /datum/movement_modifier/spaceham, src.type)

	OnRemove()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.set_body_icon_dirty()
			REMOVE_MOVEMENT_MODIFIER(H, /datum/movement_modifier/spaceham, src.type)

	OnLife()
		if(..()) return
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			if (prob(1) && !H.find_ailment_by_type(/datum/ailment/malady/heartdisease))
				H.contract_disease(/datum/ailment/malady/heartdisease,null,null,1)
		return

/datum/bioEffect/shortsighted
	name = "Diminished Optic Nerves"
	desc = "Reduces the subject's ability to see clearly without glasses or other visual aids."
	id = "bad_eyesight"
	effectType = effectTypeDisability
	probability = 99
	isBad = 1
	msgGain = "Your vision blurs."
	msgLose = "Your vision is no longer blurry."
	reclaim_fail = 15
	stability_loss = -5
	var/datum/hud/vision_impair/hud = new
	var/applied = 1
	icon_state  = "bad"

	OnAdd()
		..()
		owner.attach_hud(src.hud)

	OnRemove()
		..()
		owner.detach_hud(src.hud)

	OnLife()
		if(..()) return
		if (owner.client && ishuman(owner) && owner.sight_check(1))
			var/mob/living/carbon/human/H = owner
			var/corrected_vision = 0
			if (H.glasses) // Marq fix for cannot read null.correct_bad_vision
				if (H.glasses.correct_bad_vision)
					corrected_vision = 1

			if (!corrected_vision)
				if (!applied)
					owner.attach_hud(src.hud)
					applied = 1
			else
				if (applied)
					owner.detach_hud(src.hud)
					applied = 0
		return

/datum/bioEffect/epilepsy
	name = "Epilepsy"
	desc = "Causes damage to the subject's brain structure, resulting in occasional siezures from brain misfires."
	id = "epilepsy"
	effectType = effectTypeDisability
	isBad = 1
	probability = 66
	blockCount = 3
	msgGain = "Your thoughts become disorderly and hard to control."
	msgLose = "Your mind regains its former clarity."
	reclaim_fail = 15
	stability_loss = -10
	icon_state  = "bad"

	OnLife()
		if(..()) return
		if (isdead(owner))
			return
		if (prob(1) && !owner.getStatusDuration("paralysis"))
			owner:visible_message("<span class='alert'><B>[owner] starts having a seizure!</span>", "<span class='alert'>You have a seizure!</span>")
			owner.setStatus("paralysis", max(owner.getStatusDuration("paralysis"), 20))
			owner:make_jittery(100)
		return

/datum/bioEffect/thermal_vuln
	name = "Thermal Vulnerability"
	desc = "Cripples the subject's thermoregulation, rendering them more vulnerable to abnormal temperatures."
	id = "thermal_vuln"
	effectType = effectTypeDisability
	isBad = 1
	probability = 40
	blockCount = 3
	msgGain = "You break out into a cold sweat."
	msgLose = "The air feels more comfortable again."
	reclaim_fail = 15
	stability_loss = -20
	icon_state  = "bad"

	OnAdd()
		..()
		owner.temp_tolerance *= 0.25
		owner.thermoregulation_mult *= 0.5
		owner.innate_temp_resistance *= 2

	OnRemove()
		..()
		owner.temp_tolerance *= 4
		owner.thermoregulation_mult *= 2
		owner.innate_temp_resistance *= 0.5

/datum/bioEffect/toxification
	name = "Blood Toxification"
	desc = "Impairs the subject's blood filtration, resulting in gradual toxic buildup that must be purged by outside assistance."
	id = "toxification"
	effectType = effectTypeDisability
	isBad = 1
	probability = 13
	blockCount = 3
	msgGain = "You feel really sick..."
	msgLose = "You don't feel sick any more."
	reclaim_fail = 30
	stability_loss = -20
	var/tox_amount = 1
	var/tox_prob = 10
	icon_state  = "bad"

	OnLife()
		if(..()) return
		if (iscarbon(owner))
			var/mob/living/carbon/C = owner
			if (prob(tox_prob))
				C.toxloss += tox_amount

/datum/bioEffect/tourettes
	name = "Tourettes"
	desc = "Alters the subject's brain structure, causing periodic involuntary movements and outbursts."
	id = "tourettes"
	effectType = effectTypeDisability
	isBad = 1
	probability = 66
	msgGain = "You feel like you can't control your actions fully."
	msgLose = "You feel in full control of yourself once again."
	reclaim_fail = 15
	stability_loss = -5
	icon_state  = "bad"

	OnLife()
		if(..()) return
		if (isdead(owner))
			return
		if ((prob(10) && !owner.getStatusDuration("paralysis")))
			owner.changeStatus("stunned", 3 SECONDS)
			SPAWN_DBG( 0 )
				switch(rand(1, 3))
					if (1 to 2)
						owner.emote("twitch")
					if (3)
						if (owner.client)
							var/enteredtext = winget(owner, "mainwindow.input", "text")
							if ((copytext(enteredtext,1,6) == "say \"") && length(enteredtext) > 5)
								winset(owner, "mainwindow.input", "text=\"\"")
								if (prob(50))
									owner.say(uppertext(copytext(enteredtext,6,0)))
								else
									owner.say(copytext(enteredtext,6,0))
		return

/datum/bioEffect/cough
	name = "Chronic Cough"
	desc = "Enhances the sensitivity of nerves in the subject's throat, causing periodic coughing fits."
	id = "cough"
	probability = 99
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel an irritating itch in your throat."
	msgLose = "Your throat clears up."
	reclaim_fail = 15
	icon_state  = "bad"

	OnLife()
		if(..()) return
		if (isdead(owner))
			return
		if ((prob(5) && !owner.getStatusDuration("paralysis")))
			owner:drop_item()
			SPAWN_DBG (0)
				owner:emote("cough")
				return
		return

#define LIMB_IS_ARM 1
#define LIMB_IS_LEG 2
/datum/bioEffect/funky_limb
	name = "Motor Neuron Signal Enhancement" // heh
	desc = "Causes involuntary muscle contractions in limbs, due to a loss of inhibition of motor neurons."
	id = "funky_limb"
	effectType = effectTypeDisability
	isBad = 1
	probability = 33
	blockCount = 4
	msgGain = "One of your limbs feels a bit strange and twitchy."
	msgLose = "Your limb feels fine again."
	reclaim_fail = 15
	stability_loss = -20
	lockProb = 50
	lockedGaps = 2
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	var/obj/item/parts/limb = null
	var/limb_type = LIMB_IS_ARM
	icon_state  = "bad"

	OnAdd()
		..()
		if (!ishuman(owner))
			return
		var/mob/living/carbon/human/H = owner
		var/list/possible_limbs = list()
		if (H.limbs.l_arm)
			possible_limbs += H.limbs.l_arm
		if (H.limbs.r_arm)
			possible_limbs += H.limbs.r_arm
		if (H.limbs.l_leg)
			possible_limbs += H.limbs.l_leg
		if (H.limbs.r_leg)
			possible_limbs += H.limbs.r_leg
		if (!possible_limbs.len)
			return

		src.limb = pick(possible_limbs)

		if (istype(src.limb, /obj/item/parts/human_parts/arm) || istype(src.limb, /obj/item/parts/robot_parts/arm))
			src.limb_type = LIMB_IS_ARM
			return
		else if (istype(src.limb, /obj/item/parts/human_parts/leg) || istype(src.limb, /obj/item/parts/robot_parts/leg))
			src.limb_type = LIMB_IS_LEG
			return

	OnLife()
		if(..()) return
		if (!src.limb || (src.limb.loc != src.owner))
			return
		if (owner.stat)
			return

		if (src.limb_type == LIMB_IS_ARM)
			if (prob(5))
				owner.visible_message("<span class='alert'>[owner.name]'s [src.limb] makes a [pick("rude", "funny", "weird", "lewd", "strange", "offensive", "cruel", "furious")] gesture!</span>")
			else if (prob(2))
				owner.emote("slap")
			else if (prob(2))
				owner.visible_message("<span class='alert'><B>[owner.name]'s [src.limb] punches [him_or_her(owner)] in the face!</B></span>")
				owner.TakeDamageAccountArmor("head", rand(2,5), 0, 0, DAMAGE_BLUNT)
			else if (prob(1))
				owner.visible_message("<span class='alert'>[owner.name]'s [src.limb] tries to strangle [him_or_her(owner)]!</span>")
				while (prob(80) && owner.bioHolder.HasEffect("funky_limb"))
					owner.losebreath = max(owner.losebreath, 2)
					sleep(1 SECOND)
				owner.visible_message("<span class='alert'>[owner.name]'s [src.limb] stops trying to strangle [him_or_her(owner)].</span>")
			return

		else if (src.limb_type == LIMB_IS_LEG)
			if (prob(5))
				owner.visible_message("<span class='alert'>[owner.name]'s [src.limb] twitches [pick("rudely", "awkwardly", "weirdly", "lewdly", "strangely", "offensively", "cruelly", "furiously")]!</span>")
			else if (prob(3))
				owner.visible_message("<span class='alert'><B>[owner.name] trips over [his_or_her(owner)] own [src.limb]!</B></span>")
				owner.changeStatus("weakened", 2 SECONDS)
			else if (prob(2))
				owner.visible_message("<span class='alert'><B>[owner.name]'s [src.limb] kicks [him_or_her(owner)] in the head somehow!</B></span>")
				owner.changeStatus("paralysis", 70)
				owner.TakeDamageAccountArmor("head", rand(5,10), 0, 0, DAMAGE_BLUNT)
			else if (prob(2))
				owner.visible_message("<span class='alert'><B>[owner.name] can't seem to control [his_or_her(owner)] [src.limb]!</B></span>")
				owner.change_misstep_chance(10)
			return

#undef LIMB_IS_ARM
#undef LIMB_IS_LEG

///////////////////////////////////////
// Harmful to others as well as self //
///////////////////////////////////////

/datum/bioEffect/radioactive
	name = "Radioactive"
	desc = "The subject suffers from constant radiation sickness and causes the same on nearby organics."
	id = "radioactive"
	effectType = effectTypeDisability
	probability = 66
	blockCount = 3
	blockGaps = 3
	isBad = 1
	stability_loss = 10
	msgGain = "You feel a strange sickness permeate your whole body."
	msgLose = "You no longer feel awful and sick all over."
	reclaim_fail = 15
	icon_state  = "bad"

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "aurapulse", layer = MOB_LIMB_LAYER)
			overlay_image.color = "#BBD90F"
		..()

	OnLife()
		if(..()) return
		owner.changeStatus("radiation", 30, 1)
		for(var/mob/living/L in range(1, owner))
			if (L == owner)
				continue
			boutput(L, "<span class='alert'>You are enveloped by a soft green glow emanating from [owner].</span>")
			L.changeStatus("radiation", 50, 1)
		return

/datum/bioEffect/mutagenic_field
	name = "Mutagenic Field"
	desc = "The subject emits low-level radiation that may cause everyone in range to mutate."
	id = "mutagenic_field"
	effectType = effectTypeDisability
	isBad = 1
	probability = 33
	blockCount = 3
	blockGaps = 4
	msgGain = "Your flesh begins to warp and contort weirdly!"
	msgLose = "You stop warping and contorting. Phew, what a relief..."
	reclaim_fail = 50
	lockProb = 50
	lockedGaps = 2
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	stability_loss = 50
	var/affect_others = 0
	var/field_range = 2
	var/proc_prob = 5
	var/mutation_type = "either"
	icon_state  = "bad"

	New()
		..()
		if (prob(25))
			mutation_type = "bad"
			if (prob(5))
				mutation_type = "good"

	OnLife()
		if(..()) return
		if (prob(proc_prob))
			owner.bioHolder.RandomEffect(mutation_type,1)
			if (affect_others)
				for(var/mob/living/L in range(field_range, get_turf(owner)))
					if (!L.bioHolder)
						continue
					L.bioHolder.RandomEffect(mutation_type,1)
				return

/datum/bioEffect/involuntary_teleporting
	name = "Spatial Destabilization"
	desc = "Causes the subject's molecular structure to become partially unstuck in space."
	id = "involuntary_teleporting"
	effectType = effectTypeDisability
	isBad = 1
	probability = 33
	blockCount = 3
	blockGaps = 4
	msgGain = "You feel a bit out of place."
	msgLose = "You feel firmly rooted in place again."
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 3
	lockedChars = list("G","C","A","T")
	lockedTries = 8
	stability_loss = 15
	var/tele_prob = 5
	icon_state  = "bad"

	OnLife()
		if(..()) return
		var/mob/living/L = owner
		if (!isturf(L.loc))
			return

		if (isrestrictedz(L.z))
			boutput(L, "<span class='notice'>You feel quite strange. Almost as if you're not supposed to be here.</span>")
			return

		if (prob(tele_prob))
			var/list/randomturfs = new/list()
			for(var/turf/simulated/floor/T in orange(L, 10))
				randomturfs.Add(T)

			if (randomturfs.len > 0)
				L.emote("hiccup")
				L.set_loc(pick(randomturfs))

//////////////
// Annoying //
//////////////

/datum/bioEffect/emoter
	name = "Irritable Bowels"
	desc = "Causes the subject to experience frequent involuntary flatus."
	id = "farty"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "Your guts are rumbling."
	msgLose = "Your guts settle down."
	probability = 99
	var/emote_prob = 15
	var/emote_type = "fart"
	icon_state  = "bad"

	OnLife()
		if(..()) return
		var/mob/living/L = owner
		if (!L)
			return
		if (isdead(L))
			return
		if (prob(emote_prob))
			L.emote(emote_type)

/datum/bioEffect/emoter/screamer
	name = "Paranoia"
	desc = "Causes the subject to become easily startled."
	id = "screamer"
	msgGain = "They're gonna get you!!!!!"
	msgLose = "You calm down."
	emote_type = "scream"
	emote_prob = 10


/datum/bioEffect/emoter/linkedfart
	name = "Psychic Fart Link"
	desc = "Creates a neural fart linkage between all life-forms within 15 kilometers"
	id = "linkedfart"
	msgGain = "You feel the gas of a thousand souls"
	msgLose = "You no longer feel so gassy."
	emote_type = "fart"
	emote_prob = 20
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0

/datum/bioEffect/emoter/juggler
	name = "Jugglemancer's Curse"
	desc = "Places a mystical hex upon the subject that compels the subject to juggle."
	id = "juggler"
	msgGain = "You feel the need to juggle"
	msgLose = "You no longer feel the need to juggle."
	emote_type = "twirl"
	emote_prob = 35
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0

	OnAdd()
		..()
		var/mob/living/L = owner
		if (ishuman(L))
			L:can_juggle = 1

/datum/bioEffect/buzz
	name = "Nectar Perspiration"
	desc = "Causes the subject to perspire nectar that attracts abnormally small bees."
	id = "buzz"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "Your guts are rumbling."
	msgLose = "Your guts settle down."
	probability = 70
	stability_loss = -10	//maybe 5
	var/prob_sting = 10;
	icon_state  = "bad"

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "buzz", layer = MOB_EFFECT_LAYER)
		..()

	OnLife()
		var/mob/living/L = owner
		if (!istype(L) || (L.stat == 2))
			return
		if (prob(prob_sting))
			boutput(src, "<span class='alert'>A bee in your cloud stung you! How rude!</span>")
			L.reagents.add_reagent("histamine", 2)

/datum/bioEffect/emp_field
	name = "Electromagnetic Field"
	desc = "The subject produces small, random electromagnetic pulses around it."
	id = "emp_field"
	effectType = effectTypeDisability
	msgGain = "You feel more connected to electromagnetic fields."
	msgLose = "You feel less connected to electromagnetic fields."
	blockCount = 1
	probability = 20
	isBad = 1
	reclaim_fail = 15
	stability_loss = -10
	var/const/radius = 2
	icon_state  = "bad"

	OnLife()
		..()
		if (prob(50))
			return

		var/turf/T
		//don't really need this but to make it more harmful to the user.
		if (prob(5))
			T = get_turf(owner)
		else
			T = locate(owner.x + rand(-radius/2,radius+2), owner.y+rand(-radius/2,radius/2), 1)

		var/obj/overlay/pulse = new/obj/overlay(T)
		pulse.icon = 'icons/effects/effects.dmi'
		pulse.icon_state = "emppulse"
		pulse.name = "emp pulse"
		pulse.anchored = 1
		SPAWN_DBG (20)
			if (pulse) qdel(pulse)

		//maybe have this only emp some things on the tile.
		if(istype(T))
			for (var/atom/O in T.contents)
				O.emp_act()

/datum/bioEffect/fitness_debuff
	name = "Physically Unfit"
	desc = "Causes the subject to be naturally less physically fit than the average spaceman."
	id = "fitness_debuff"
	probability = 60
	isBad = 1
	effectType = effectTypePower
	blockCount = 2
	blockGaps = 3
	reclaim_mats = 30
	msgGain = "You feel slightly less energetic."
	msgLose = "You feel slightly more energetic."
	lockProb = 20
	lockedGaps = 1
	lockedDiff = 3
	lockedTries = 8
	stability_loss = 5
	icon_state  = "bad"

	OnAdd()
		src.owner.add_stam_mod_regen("g-fitness-debuff", -2)
		src.owner.add_stam_mod_max("g-fitness-debuff", -30)

	OnRemove()
		src.owner.remove_stam_mod_regen("g-fitness-debuff")
		src.owner.remove_stam_mod_max("g-fitness-debuff")

/datum/bioEffect/tinnitus
	name = "Tinnitus"
	desc = "Causes the subject to almost constantly hear a terrible/annoying ringing in their ears."
	id = "tinnitus"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You hear a ringing in your ears."
	msgLose = "The ringing has stopped...Finally. Thank the Space-Gods."
	stability_loss = -5
	probability = 99
	var/ring_prob = 6
	icon_state  = "bad"

	OnLife()
		if (prob(ring_prob) && owner.client)
			// owner.client << sound("phone-ringing.wav")		//play sound only for client. Untested, don't know the sound
			owner.client << sound("sound/machines/phones/ring_incoming.ogg")		//play sound only for client. Untested, don't know the sound

/datum/bioEffect/anemia
	name = "Anemia"
	desc = "Subject has an abnormally low amount of red blood cells."
	id = "anemia"
	probability = 55
	isBad = 1
	effectType = effectTypePower
	msgGain = "You feel lightheaded."
	msgLose = "Your lightheadedness fades."
	stability_loss = -5
	var/run = 1
	icon_state  = "bad"

	OnLife()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner

			if (H.blood_volume > 400 && H.blood_volume > 0)
				H.blood_volume -= 2

/datum/bioEffect/polycythemia
	name = "Polycythemia"
	desc = "Subject has an abnormally high amount of red blood cells."
	id = "polycythemia"
	probability = 45
	isBad = 1
	effectType = effectTypePower
	msgGain = "Your breathing quickens."
	msgLose = "Your breathing returns to normal."
	stability_loss = -5
	var/run = 1
	icon_state  = "bad"

	OnLife()

		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner

			if (H.blood_volume < 600 && H.blood_volume > 0)
				H.blood_volume += 2


////////////////////////////
// Disabled for *Reasons* //
////////////////////////////

/datum/bioEffect/mind_jockey
	name = "Meta-Neural Transferral"
	desc = "The subject's brainwaves will occasionally involuntarily switch with those of another near them."
	id = "mind_jockey"
	effectType = effectTypeDisability
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	isBad = 1
	msgGain = "You wanna be somebody else."
	msgLose = "You're happy with yourself."
	var/proc_prob = 5
	icon_state  = "bad"

	OnLife()
		if(..()) return
		if (prob(proc_prob))
			var/list/potential_victims = list()
			for(var/mob/living/carbon/human/H in range(7,owner))
				if (!H.client || H.stat)
					continue
				potential_victims += H
			if (potential_victims.len)
				var/mob/living/carbon/human/this_one = pick(potential_victims)
				boutput(src, "<span class='alert'>Your mind twangs uncomfortably!</span>")
				boutput(this_one, "<span class='alert'>Your mind twangs uncomfortably!</span>")
				owner.mind.swap_with(this_one)

/datum/bioEffect/mutagenic_field/prenerf
	name = "High-Power Mutagenic Field"
	id = "mutagenic_field_prenerf"
	affect_others = 1
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	icon_state  = "bad"

/datum/bioEffect/randomeffects
	name = "Booster Gene Q"
	desc = "This function of this gene is not well-researched."
	researched_desc = "This gene has random, unpredictable effects on the subject."
	id = "randomeffects"
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	curable_by_mutadone = 0
	blockCount = 2
	blockGaps = 4
	lockProb = 66
	lockedGaps = 1
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	stability_loss = 15
	var/prob_per_tick = 15
	var/list/emotes = list("slap","snap","hiccup","burp","fart","dance","tantrum","flipoff","flip","boggle")
	var/list/noises = list('sound/musical_instruments/WeirdHorn_0.ogg','sound/voice/animal/cat.ogg','sound/musical_instruments/Saxophone_CarelessWhisper.ogg',
	'sound/machines/engine_alert3.ogg','sound/machines/fortune_riff.ogg','sound/misc/ancientbot_grump2.ogg',
	'sound/voice/farts/diarrhea.ogg','sound/misc/sad_server_death.ogg','sound/voice/animal/werewolf_howl.ogg',
	'sound/voice/MEruncoward.ogg','sound/voice/macho/macho_become_enraged01.ogg',
	'sound/voice/macho/macho_rage_81.ogg','sound/voice/macho/macho_rage_73.ogg','sound/weapons/male_cswordstart.ogg')
	icon_state  = "bad"

	New(var/for_global_list = 0)
		..()
		if (!for_global_list)
			name = "Booster Gene"

	OnLife()
		if(..()) return
		if (prob(prob_per_tick))
			var/mob/living/L = owner
			var/picker = rand(1,5)
			switch(picker)
				if (1)
					L.HealDamage("All",10,0)
				if (2)

					if (isrestrictedz(L.z))
						boutput(L, "<span class='notice'>You feel your genes tingling inside you. Strange.</span>")
						return

					var/list/randomturfs = new/list()
					for(var/turf/simulated/floor/T in orange(L, 10))
						randomturfs.Add(T)

					if (randomturfs.len > 0)
						L.emote("hiccup")
						L.set_loc(pick(randomturfs))
				if (3)
					L.emote(pick(emotes))
				if (4)
					L.color = random_color()
					var/turf/T = get_turf(L)
					T.color = random_color()
				if (5)
					L.visible_message("<span class='alert'><b>[L.name]</b> makes a weird noise!</span>")
					playsound(L.loc, pick(noises), 50, 0)

/datum/bioEffect/sneeze
	name = "Chronic Sneezing"
	desc = "Enhances the sensitivity of nerves in the subject's nose, causing periodic sneezing."
	id = "sneeze"
	probability = 66
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel an irritating itch in your nose."
	msgLose = "Your nose clears up."
	reclaim_fail = 15
	icon_state  = "bad"

	OnLife()
		if (prob(5))
			if (isdead(owner))
				return
			else
				owner:emote("sneeze")
		return

////////////////////////////////
// Resistances and Immunities //
////////////////////////////////

/datum/bioEffect/fireres
	name = "Fire Resistance"
	desc = "Shields the subject's cellular structure against high temperatures and flames."
	id = "fire_resist"
	effectType = effectTypePower
	probability = 66
	blockCount = 3
	msgGain = "You feel cold."
	msgLose = "You feel warm."
	stability_loss = 10
	degrade_to = "aura_fire"
	icon_state  = "fire_res"

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "aurapulse", layer = MOB_LIMB_LAYER)
			overlay_image.color = "#FFA200"
		..()

/datum/bioEffect/coldres
	name = "Cold Resistance"
	desc = "Shields the subject's cellular structure against freezing temperatures and crystallization."
	id = "cold_resist"
	effectType = effectTypePower
	probability = 66
	blockCount = 3
	msgGain = "You feel warm."
	msgLose = "You feel cold."
	stability_loss = 10
	// you feel warm because you're resisting the cold, stop changing these around! =(
	degrade_to = "shiny"
	icon_state  = "cold_res"

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "aurapulse", layer = MOB_LIMB_LAYER)
			overlay_image.color = "#009DFF"
		..()

/datum/bioEffect/thermalres
	name = "Thermal Resistance"
	desc = "Shields the subject's cellular structure against any harmful temperature exposure."
	id = "thermal_resist"
	effectType = effectTypePower
	blockCount = 3
	occur_in_genepools = 0
	stability_loss = 10
	var/image/overlay_image_two = null
	degrade_to = "thermal_vuln"
	icon_state  = "thermal_res"

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "aurapulse", layer = MOB_LIMB_LAYER)
			overlay_image_two = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "aurapulse-offset", layer = MOB_LIMB_LAYER)
			overlay_image.color = "#FFA200"
			overlay_image_two.color = "#009DFF"
		..()
		if(overlay_image_two)
			var/mob/living/L = owner
			L.UpdateOverlays(overlay_image_two, id + "2")

	OnRemove()
		..()
		if(overlay_image_two)
			if(isliving(owner))
				var/mob/living/L = owner
				L.UpdateOverlays(null, id + "2")
		return

/datum/bioEffect/elecres
	name = "SMES Human"
	desc = "Protects the subject's cellular structure from electrical energy."
	id = "resist_electric"
	effectType = effectTypePower
	probability = 66
	blockCount = 3
	blockGaps = 3
	stability_loss = 15
	msgGain = "Your hair stands on end."
	msgLose = "The tingling in your skin fades."
	degrade_to = "funky_limb"
	icon_state  = "elec_res"

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "elec[owner.bioHolder?.HasEffect("fat") ? "fat" :""]", layer = MOB_EFFECT_LAYER)
		..()
		if (istype(owner, /mob/living) && owner:organHolder && owner:organHolder:heart && owner:organHolder:heart:robotic)
			owner:organHolder:heart:broken = 1
			owner:contract_disease(/datum/ailment/malady/flatline,null,null,1)
			boutput(owner, "<span class='alert'>Something is wrong with your cyberheart, it stops beating!</span>")

/datum/bioEffect/rad_resist
	name = "Radiation Resistance"
	desc = "Shields the subject's cellular structure against ionizing radiation."
	id = "food_rad_resist"
	effectType = effectTypePower
	blockCount = 2
	secret = 1
	stability_loss = 15
	degrade_to = "radioactive"
	icon_state  = "rad_res"

/datum/bioEffect/alcres
	name = "Alcohol Resistance"
	desc = "Strongly reinforces the subject's nervous system against alcoholic intoxication."
	id = "resist_alcohol"
	probability = 99
	effectType = effectTypePower
	msgGain = "You feel unusually sober."
	msgLose = "You feel like you could use a stiff drink."
	degrade_to = "drunk"
	icon_state  = "alc_res"

/datum/bioEffect/toxres
	name = "Toxic Resistance"
	desc = "Renders the subject's blood immune to toxification. This will not stop other effects of poisons from occurring, however."
	id = "resist_toxic"
	effectType = effectTypePower
	probability = 8
	blockCount = 4
	blockGaps = 5
	reclaim_mats = 40
	curable_by_mutadone = 0
	stability_loss = 40
	msgGain = "You feel refreshed and clean."
	msgLose = "You feel a bit grody."
	degrade_to = "toxification"
	icon_state  = "tox_res"

	OnAdd()
		..()
		if (!ishuman(owner))
			return
		var/mob/living/carbon/human/H = owner
		H.toxloss = 0

/datum/bioEffect/breathless
	name = "Anaerobic Metabolism"
	desc = "Allows the subject's body to generate its own oxygen internally, invalidating the need for respiration."
	id = "breathless"
	effectType = effectTypePower
	probability = 8
	blockCount = 4
	blockGaps = 2
	reclaim_mats = 40
	lockProb = 66
	lockedGaps = 3
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	stability_loss = 40
	msgGain = "Your lungs feel strangely empty."
	msgLose = "You start gasping for air."
	degrade_to = "mute"
	icon_state  = "breathless"

	OnAdd()
		..()
		if (!ishuman(owner))
			return
		var/mob/living/carbon/human/H = owner
		H.oxyloss = 0
		H.losebreath = 0

/datum/bioEffect/breathless/contract
	name = "Airless Breathing"
	id = "breathless_contract"
	msgGain = ""
	msgLose = ""
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	stability_loss = 0

/datum/bioEffect/psychic_resist
	name = "Meta-Neural Enhancement"
	desc = "Boosts efficiency in sectors of the brain commonly associated with resisting meta-mental energies."
	id = "psy_resist"
	probability = 99
	effectType = effectTypePower
	msgGain = "Your mind feels closed."
	msgLose = "You feel oddly exposed."
	degrade_to = "screamer"

/////////////
// Healing //
/////////////

/datum/bioEffect/regenerator
	name = "Regeneration"
	desc = "Overcharges the subject's natural healing, enabling them to rapidly heal from any wound."
	id = "regenerator"
	effectType = effectTypePower
	probability = 8
	blockCount = 4
	blockGaps = 3
	reclaim_mats = 40
	lockProb = 66
	lockedGaps = 1
	lockedDiff = 4
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	stability_loss = 40
	msgGain = "Your skin feels tingly and shifty."
	msgLose = "Your skin tightens."
	var/heal_per_tick = 1
	var/regrow_prob = 250
	degrade_to = "mutagenic_field"
	icon_state  = "regen"

	OnLife()
		if(..()) return
		var/mob/living/L = owner
		L.HealDamage("All", heal_per_tick, heal_per_tick)
		if (rand(1,regrow_prob) == 1 && ishuman(L))
			var/mob/living/carbon/human/H = L
			if (H.limbs)
				H.limbs.mend(1)

/datum/bioEffect/regenerator/super
	name = "Super Regeneration"
	desc = "Subject's cells are capable of repairing immense trauama at an unbelievably rapid rate."
	id = "regenerator_super"
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	stability_loss = 0 //necessary to prevent issues with existing mutants who sign the contract. We want the sleep to be their downfall, not their genes.
	msgGain = "You begin to feel your flesh mending back together. Grody."
	msgLose = "Your flesh stops mending itself together."
	heal_per_tick = 7 // decrease to 5 if extreme narcolepsy doesn't counterbalance this enough
	regrow_prob = 50 //increase to 100 if not counterbalanced


/datum/bioEffect/detox
	name = "Natural Anti-Toxins"
	desc = "Enables the subject's bloodstream to purge foreign substances more rapidly."
	id = "detox"
	probability = 66
	blockCount = 2
	blockGaps = 4
	msgGain = "Your pulse seems to relax."
	msgLose = "Your pulse quickens."
	lockProb = 66
	lockedGaps = 3
	lockedDiff = 2
	lockedChars = list("G","C","A","T")
	lockedTries = 10
	curable_by_mutadone = 0
	var/remove_per_tick = 5
	stability_loss = 15
	degrade_to = "toxification"
	icon_state  = "tox_res"

	OnLife()
		if(..()) return
		var/mob/living/L = owner
		if (isdead(L))
			return
		if (L.reagents)
			L.reagents.remove_any(remove_per_tick)

/////////////
// Stealth //
/////////////

/datum/bioEffect/examine_stopper
	name = "Meta-Neural Haze"
	desc = "Causes the subject's brain to emit waves that make the subject's body difficult to observe."
	id = "examine_stopper"
	effectType = effectTypePower
	secret = 1
	blockCount = 3
	blockGaps = 3
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 4
	lockedChars = list("A","T")
	lockedTries = 6
	stability_loss = 5
	icon_state  = "haze"
	isBad = 1

/datum/bioEffect/dead_scan
	name = "Pseudonecrosis"
	desc = "Causes the subject's cells to mimic a death-like state."
	id = "dead_scan"
	effectType = effectTypePower
	probability = 99
	stability_loss = 5
	icon_state  = "dead"
	isBad = 1

///////////////////
// General buffs //
///////////////////

/datum/bioEffect/strong
	name = "Musculature Enhancement"
	desc = "Enhances the subject's ability to build and retain heavy muscles."
	id = "strong"
	effectType = effectTypePower
	probability = 99
	msgGain = "You feel buff!"
	msgLose = "You feel wimpy and weak."
	icon_state  = "strong"

	OnAdd()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			APPLY_MOVEMENT_MODIFIER(H, /datum/movement_modifier/hulkstrong, src.type)

	OnRemove()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			REMOVE_MOVEMENT_MODIFIER(H, /datum/movement_modifier/hulkstrong, src.type)

/datum/bioEffect/radio_brain
	name = "Meta-Neural Antenna"
	desc = "Enables the subject's brain to pick up radio signals."
	id = "radio_brain"
	effectType = effectTypePower
	probability = 33
	blockCount = 4
	blockGaps = 5
	reclaim_mats = 30
	msgGain = "You can hear weird chatter in your head."
	msgLose = "The weird noise in your head stops."
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 3
	lockedChars = list("G","C","A","T")
	lockedTries = 8
	stability_loss = 5
	degrade_to = "involuntary_teleporting"
	icon_state  = "radiobrain"

	OnAdd()
		radio_brains += owner

	OnRemove()
		radio_brains -= owner

var/list/radio_brains = list()

/datum/bioEffect/hulk
	name = "Gamma Ray Exposure"
	desc = "Vastly enhances the subject's musculature. May cause severe melanocyte corruption."
	id = "hulk"
	effectType = effectTypePower
	probability = 33
	blockCount = 4
	blockGaps = 5
	reclaim_mats = 30
	msgGain = "You feel your muscles swell to an immense size."
	msgLose = "Your muscles shrink back down."
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 3
	lockedChars = list("G","C","A","T")
	lockedTries = 8
	stability_loss = 25
	degrade_to = "fat"
	icon_state  = "hulk"

	OnAdd()
		owner.unlock_medal("It's not easy being green", 1)
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.set_body_icon_dirty()
			APPLY_MOVEMENT_MODIFIER(H, /datum/movement_modifier/hulkstrong, src.type)
		..()

	OnMobDraw()
		if(disposed)
			return

		if(ishuman(owner))
			owner:body_standing:overlays += image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "hulk[owner.bioHolder?.HasEffect("fat") ? "fat" :""]", layer = MOB_LAYER)

	OnRemove()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.set_body_icon_dirty()
			REMOVE_MOVEMENT_MODIFIER(H, /datum/movement_modifier/hulkstrong, src.type)

	OnLife()
		if(..()) return
		if (owner:health <= 25)
			timeLeft = 1
			boutput(owner, "<span class='alert'>You suddenly feel very weak.</span>")
			owner:changeStatus("weakened", 3 SECONDS)
			owner:emote("collapse")

/datum/bioEffect/xray
	name = "X-Ray Vision"
	desc = "Enhances the subject's optic nerves, allowing them to see on x-ray wavelengths."
	id = "xray"
	effectType = effectTypePower
	probability = 33
	blockCount = 3
	blockGaps = 5
	reclaim_mats = 40
	msgGain = "You suddenly seem to be able to see through everything."
	msgLose = "Your vision fades back to normal."
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 3
	lockedChars = list("G","C","A","T")
	lockedTries = 8
	stability_loss = 30
	degrade_to = "bad_eyesight"
	icon_state  = "eye"

/datum/bioEffect/nightvision
	name = "Night Vision"
	desc = "Enhances the subject's optic nerves, allowing them to see in the dark."
	id = "nightvision"
	effectType = effectTypePower
	probability = 33
	blockCount = 3
	blockGaps = 3
	reclaim_mats = 30
	msgGain = "Everything suddenly appears oddly lit."
	msgLose = "You blink and something seems to vanish."
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 3
	lockedChars = list("G","C","A","T")
	lockedTries = 8
	stability_loss = 25
	degrade_to = "bad_eyesight"
	icon_state  = "eye"

/datum/bioEffect/toxic_farts
	name = "High Decay Digestion"
	desc = "Causes the subject's digestion to create a significant amount of noxious gas."
	id = "toxic_farts"
	probability = 33
	blockCount = 2
	blockGaps = 4
	msgGain = "Your stomach grumbles unpleasantly."
	msgLose = "Your stomach stops acting up. Phew!"
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 3
	lockedChars = list("G","C","A","T")
	lockedTries = 8
	stability_loss = 10
	degrade_to = "stinky"
	icon_state  = "fart"

/datum/bioEffect/fitness_buff
	name = "Physically Fit"
	desc = "Causes the subject to be naturally more physically fit than the average spaceman."
	id = "fitness_buff"
	effectType = effectTypePower
	probability = 50
	blockCount = 2
	blockGaps = 3
	reclaim_mats = 30
	msgGain = "You feel slightly more energetic."
	msgLose = "You feel slightly less energetic."
	lockProb = 20
	lockedGaps = 1
	lockedDiff = 3
	lockedTries = 8
	stability_loss = -5
	icon_state  = "strong"

	OnAdd()
		src.owner.add_stam_mod_regen("g-fitness-buff", 2)
		src.owner.add_stam_mod_max("g-fitness-buff", 30)

	OnRemove()
		src.owner.remove_stam_mod_regen("g-fitness-buff")
		src.owner.remove_stam_mod_max("g-fitness-buff")

/datum/bioEffect/blood_overdrive
	name = "Hemopoiesis Overdrive"
	desc = "Subject has regenerates blood far faster than the average spaceman."
	id = "blood_overdrive"
	probability = 20
	effectType = effectTypePower
	msgGain = "You feel like being stabbed isn't such a big deal anymore."
	msgLose = "You are once again afraid of being stabbed."
	stability_loss = 15
	icon_state  = "regen"

	OnLife()

		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner

			if (H.blood_volume < 500 && H.blood_volume > 0)
				H.blood_volume += 6


///////////////////////////
// Disabled/Inaccessible //
///////////////////////////

/datum/bioEffect/telekinesis
	name = "Telekinesis"
	desc = "Enables the subject to project kinetic energy using certain thought patterns."
	id = "telekinesis"
	effectType = effectTypePower
	probability = 8
	blockCount = 5
	blockGaps = 5
	reclaim_mats = 40
	msgGain = "You feel your consciousness expand outwards."
	msgLose = "Your conciousness closes inwards."
	stability_loss = 30
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	icon_state  = "tk"

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "telekinesishead[owner.bioHolder?.HasEffect("fat") ? "fat" :""]", layer = MOB_LAYER)
		..()

/datum/bioEffect/uncontrollable_cloak
	name = "Unstable Refraction"
	desc = "The subject will occasionally become invisible. The subject has no control or awareness of this occurring."
	id = "uncontrollable_cloak"
	effectType = effectTypePower
	occur_in_genepools = 0 // needs nerfing before it can be put back
	probability = 0 // formerly 66
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0

	blockCount = 3
	blockGaps = 3
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 4
	lockedChars = list("A","T")
	lockedTries = 6
	var/active = 0
	stability_loss = 15
	icon_state  = "haze"

	OnLife()
		if (prob(20))
			src.active = !src.active
		if (src.active)
			owner.invisibility = 1
		return

/datum/bioEffect/mutantrace
	name = "Saurian Genetics"
	desc = "Enables vestigal non-mammal traits in the subject's body."
	id = "lizard"
	mutantrace_option = "Lizard"
	effectType = effectTypeMutantRace
	probability = 33
	msgGain = "Your skin feels oddly dry."
	msgLose = "Your scales fall off."
	mob_exclusive = /mob/living/carbon/human/
	var/mutantrace_path = /datum/mutantrace/lizard
	lockProb = 33
	lockedGaps = 1
	lockedDiff = 4
	lockedChars = list("G","C")
	lockedTries = 8
	curable_by_mutadone = 0
	icon_state  = "lizard"

	OnAdd()
		..() // caaaaaaall yooooooour paaaareeeeents
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			for (var/ID in H.bioHolder.effects)
				// clear away any existing mutantraces first
				if (istype(H.bioHolder.GetEffect(ID), /datum/bioEffect/mutantrace) && ID != src.id)
					H.bioHolder.RemoveEffect(ID)
			H.set_mutantrace(src.mutantrace_path)
		return

	OnRemove()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			if (istype(H.mutantrace,src.mutantrace_path))
				H.set_mutantrace(null)
		return

	OnLife()
		if(..()) return
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			if (!istype(H.mutantrace, src.mutantrace_path))
				holder.RemoveEffect(id)
		return

/datum/bioEffect/mutantrace/flashy
	name = "Bioluminescent Overdrive"
	desc = "Enables highly bioluminescent cells in the subject's skin."
	id = "flashman"
	mutantrace_option = "Flashy"
	mutantrace_path = /datum/mutantrace/flashy
	msgGain = "Your skin begins flashing!"
	msgLose = "Your flashy glow fades away."
	icon_state  = "flashy"

/datum/bioEffect/mutantrace/blank
	name = "Melanin Eraser"
	desc = "Shuts down all melanin production in subject's body, and eradicates all existing melanin."
	id = "blankman"
	mutantrace_option = "Blank"
	mutantrace_path = /datum/mutantrace/blank
	msgGain = "You feel oddly plain."
	msgLose = "You don't feel boring anymore."
	icon_state  = "blank"

/datum/bioEffect/mutantrace/skeleton
	name = "Ossification"
	desc = "Compacts the subject's living tissues into their skeleton. This is somehow not fatal."
	id = "skeleton"
	mutantrace_option = "Skeleton"
	mutantrace_path = /datum/mutantrace/skeleton
	msgGain = "You feel kinda thin."
	msgLose = "You've put on a bit more weight."
	icon_state  = "skeleton"
	isBad = 1

/datum/bioEffect/mutantrace/ithillid
	name = "Aquatic Genetics"
	desc = "Re-enables ancient vestigal genes in the subject's body."
	id = "ithillid"
	mutantrace_option = "Squid"
	mutantrace_path = /datum/mutantrace/ithillid
	msgGain = "You feel wet and squishy."
	msgLose = "You feel dry."
	icon_state  = "squid"

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "squidhead", layer = MOB_HAIR_LAYER2)
		..()

/datum/bioEffect/mutantrace/dwarf
	name = "Dwarfism"
	desc = "Greatly reduces the overall size of the subject, resulting in markedly dimished height."
	id = "dwarf"
	mutantrace_option = "Dwarf"
	mutantrace_path = /datum/mutantrace/dwarf
	msgGain = "Did everything just get bigger?"
	msgLose = "You feel tall!"
	icon_state  = "dwarf"

/datum/bioEffect/mutantrace/roach
	name = "Blattodean Genetics"
	desc = "Re-enables ancient vestigal genes in the subject's body."
	id = "roach"
	mutantrace_option = "Roach"
	mutantrace_path = /datum/mutantrace/roach
	msgGain = "You feel like crawling into somewhere nice and dark."
	msgLose = "You shed your roachy skin!"
	icon_state  = "roach"

/datum/bioEffect/mutantrace/monkey
	name = "Primal Genetics"
	desc = "Enables and exaggerates vestigal ape traits."
	id = "monkey"
	mutantrace_option = "Monkey"
	mutantrace_path = /datum/mutantrace/monkey
	research_level = 3
	msgGain = "You go bananas!"
	msgLose = "You do the evolution."
	icon_state  = "monkey"

/datum/bioEffect/mutantrace/seamonkey
	name = "Aquatic Primal Genetics"
	desc = "Enables and exaggerates vestigal aquatic ape traits."
	id = "seamonkey"
	mutantrace_option = "Seamonkey"
	mutantrace_path = /datum/mutantrace/monkey/seamonkey
	msgGain = "You go bananas!"
	msgLose = "You do the evolution."
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	reclaim_fail = 100
	icon_state  = "monkey"

/datum/bioEffect/mutantrace/cat
	name = "Feline Genetics"
	desc = "Morphs the subject's traits to appear more feline in nature."
	id = "cat"
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	reclaim_fail = 100
	stability_loss = 25
	mutantrace_option = "Cat"
	mutantrace_path = /datum/mutantrace/cat
	msgGain = "You feel especially hairy."
	msgLose = "Your fur falls out."
	icon_state  = "cat"


/datum/bioEffect/mutantrace/cow
	name = "Bovine Genetics"
	desc = "The subject takes on the appearance of a domesticated space cow and gains milk production."
	id = "cow"
	mutantrace_option = "Cow"
	mutantrace_path = /datum/mutantrace/cow
	msgGain = "You feel like you're ready for some Cow RP."
	msgLose = "Your udders fall off!"
	icon_state  = "cow"

/datum/bioEffect/power
	name = "Cryokinesis"
	desc = "Allows the subject to control ice and cold."
	id = "cryokinesis"
	msgGain = "You notice a strange cold tingle in your fingertips."
	msgLose = "Your fingers feel warmer."
	effectType = effectTypePower
	cooldown = 600
	probability = 66
	blockCount = 3
	blockGaps = 2
	stability_loss = 10
	var/using = 0
	var/safety = 0
	var/power = 0
	var/ability_path = /datum/targetable/geneticsAbility/cryokinesis
	var/datum/targetable/geneticsAbility/ability = /datum/targetable/geneticsAbility/cryokinesis

	New()
		..()
		check_ability_owner()

	disposing()
		src.owner = null
		if (ability)
			ability.dispose()
			ability.owner = null
		src.ability = null
		..()

	OnAdd()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.hud.update_ability_hotbar()
			check_ability_owner()
		return

	OnRemove()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			if (H.hud)
				H.hud.update_ability_hotbar()
		return

	proc/check_ability_owner()
		if (ispath(ability_path))
			var/datum/targetable/geneticsAbility/AB = new ability_path(src)
			ability = AB
			AB.linked_power = src
			icon = AB.icon
			icon_state = AB.icon_state
			SPAWN_DBG(0)
				AB.owner = src.owner

/datum/targetable/geneticsAbility/cryokinesis
	name = "Cryokinesis"
	desc = "Exert control over cold and ice."
	icon_state = "cryokinesis"
	targeted = 1
	target_anything = 1

	cast(atom/target)
		if (..())
			return 1

		var/turf/T = get_turf(target)

		target.visible_message("<span class='alert'><b>[owner]</b> points at [target]!</span>")
		playsound(target.loc, "sound/effects/bamf.ogg", 50, 0)
		particleMaster.SpawnSystem(new /datum/particleSystem/tele_wand(get_turf(target),"8x8snowflake","#88FFFF"))

		var/obj/decal/icefloor/B
		B = new /obj/decal/icefloor(T)
		SPAWN_DBG(80 SECONDS)
			if (B)
				B.dispose()

		if (linked_power.power)
			for (var/turf/TF in orange(1,T))
				B = new /obj/decal/icefloor(TF)
				SPAWN_DBG(80 SECONDS)
					B.dispose()

		for (var/mob/living/L in T.contents)
			if (L == owner && linked_power.safety)
				continue
			boutput(L, "<span class='notice'>You are struck by a burst of ice cold air!</span>")
			if(L.getStatusDuration("burning"))
				L.delStatus("burning")
			L.bodytemperature = 100
			if (linked_power.power)
				new /obj/icecube(get_turf(L), L)

		return

	cast_misfire(atom/target)
		if (..())
			return 1

		owner.visible_message("<span class='alert'><b>[owner]</b> points at [target]!</span>")
		playsound(owner.loc, "sound/effects/bamf.ogg", 50, 0)
		particleMaster.SpawnSystem(new /datum/particleSystem/tele_wand(get_turf(owner),"8x8snowflake","#88FFFF"))

		if (!linked_power.safety)
			boutput(owner, "<span class='alert'>Your cryokinesis misfires and freezes you!</span>")
			if(owner.getStatusDuration("burning"))
				owner.delStatus("burning")
			owner.bodytemperature = 100
			new /obj/icecube(get_turf(owner), owner)
		else
			boutput(owner, "<span class='alert'>Your cryokinesis misfires!</span>")
			if(owner.getStatusDuration("burning"))
				owner.delStatus("burning")

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/mattereater
	name = "Matter Eater"
	desc = "Allows the subject to eat just about anything without harm."
	id = "mattereater"
	msgGain = "You feel hungry."
	msgLose = "You don't feel quite so hungry anymore."
	cooldown = 300
	probability = 66
	blockCount = 4
	blockGaps = 2
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/mattereater
	var/target_path = "/obj/item/"

/datum/targetable/geneticsAbility/mattereater
	name = "Matter Eater"
	desc = "Eat just about anything!"
	icon_state = "mattereater"
	targeted = 0
	var/using = 0

	cast()
		if (..())
			return 1
		if (using)
			return 1
		using = 1

		var/datum/bioEffect/power/mattereater/ME = linked_power
		var/base_path = text2path("[ME.target_path]")
		if (!ispath(base_path))
			base_path = /obj/item/
		var/list/items = get_filtered_atoms_in_touch_range(owner,base_path)
		if (ismob(owner.loc) || istype(owner.loc,/obj/))
			for (var/atom/A in owner.loc.contents)
				if (istype(A,ME.target_path))
					items += A

		if (linked_power.power)
			items += get_filtered_atoms_in_touch_range(owner, /obj/the_server_ingame_whoa)
			//So people can still get the meat ending

		if (!items.len)
			boutput(usr, "/red You can't find anything nearby to eat.")
			using = 0
			return

		var/obj/the_item = input("Which item do you want to eat?","Matter Eater") as null|obj in items
		if (!the_item)
			using = 0
			return 1

		var/area/cur_area = get_area(owner)
		var/turf/cur_turf = get_turf(owner)
		if (isrestrictedz(cur_turf.z) && !cur_area.may_eat_here_in_restricted_z && (!owner.client || !owner.client.holder))
			owner.show_text("<span class='alert'>Man, this place really did a number on your appetite. You can't bring yourself to eat anything here.</span>")
			using = 0
			return 1

		if (istype(the_item, /obj/the_server_ingame_whoa))
			var/obj/the_server_ingame_whoa/the_server = the_item
			the_server.eaten(owner)
			using = 0
			return
		owner.visible_message("<span class='alert'>[owner] eats [the_item].</span>")
		playsound(owner.loc, "sound/items/eatfood.ogg", 50, 0)

		qdel(the_item)

		if (ishuman(owner))
			for(var/A in owner.organs)
				var/obj/item/affecting = null
				if (!owner.organs[A])    continue
				affecting = owner.organs[A]
				if (!isitem(affecting))
					continue
				affecting.heal_damage(4, 0)
			owner.UpdateDamageIcon()

		using = 0
		return



	cast_misfire()
		if (..())
			return 1
		if (using)
			return 1
		using = 1

		var/datum/bioEffect/power/mattereater/ME = linked_power
		var/base_path = text2path("[ME.target_path]")
		if (!ispath(base_path))
			base_path = /obj/item/
		var/list/items = get_filtered_atoms_in_touch_range(owner,base_path)
		if (ismob(owner.loc) || istype(owner.loc,/obj/))
			for (var/atom/A in owner.loc.contents)
				if (istype(A,ME.target_path))
					items += A

		if (linked_power.power)
			items += get_filtered_atoms_in_touch_range(owner, /obj/the_server_ingame_whoa)
			//So people can still get the meat ending

		if (!items.len)
			boutput(usr, "/red You can't find anything nearby to eat.")
			using = 0
			return

		var/obj/the_item = input("Which item do you want to eat?","Matter Eater") as null|obj in items
		if (!the_item)
			using = 0
			return 1

		var/area/cur_area = get_area(owner)
		var/turf/cur_turf = get_turf(owner)
		if (isrestrictedz(cur_turf.z) && !cur_area.may_eat_here_in_restricted_z && (!owner.client || !owner.client.holder))
			owner.show_text("<span class='alert'>Man, this place really did a number on your appetite. You can't bring yourself to eat anything here.</span>")
			using = 0
			return 1

		if (istype(the_item, /obj/the_server_ingame_whoa))
			var/obj/the_server_ingame_whoa/the_server = the_item
			the_server.eaten(owner)
			using = 0
			return
		owner.visible_message("<span class='alert'>[owner] tries to swallow [the_item] whole and nearly chokes on it.</span>")
		playsound(owner.loc, "sound/items/eatfood.ogg", 50, 0)
		playsound(owner.loc, "sound/misc/meat_plop.ogg", 50, 0)
		using = 0
		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/jumpy
	name = "Jumpy"
	desc = "Allows the subject to leap great distances."
	id = "jumpy"
	msgGain = "Your leg muscles feel taut and strong."
	msgLose = "Your leg muscles shrink back to normal."
	cooldown = 30
	probability = 99
	blockCount = 4
	blockGaps = 2
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/jumpy

/datum/bioEffect/power/jumpy/jumpsuit // granted by the frog jumpsuit
	id = "jumpy_suit"
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	curable_by_mutadone = 0
	can_reclaim = 0
	can_scramble = 0
	can_research = 0
	can_make_injector = 0
	reclaim_fail = 100

/datum/targetable/geneticsAbility/jumpy
	name = "Jumpy"
	desc = "Take a big leap forward."
	icon_state = "jumpy"
	targeted = 0

	cast()
		if (..())
			return 1

		if (ismob(owner.loc))
			boutput(usr, "<span class='alert'>You can't jump right now!</span>")
			return 1

		var/jump_tiles = 10
		var/pixel_move = 8
		var/sleep_time = 1
		if (linked_power.power)
			jump_tiles = 20
			pixel_move = 16
			sleep_time = 0.5

		if (istype(owner.loc,/turf/))
			usr.visible_message("<span class='alert'><b>[owner]</b> takes a huge leap!</span>")
			playsound(owner.loc, "sound/impact_sounds/Generic_Shove_1.ogg", 50, 1)
			var/prevLayer = owner.layer
			owner.layer = EFFECTS_LAYER_BASE

			SPAWN_DBG(0)
				for(var/i=0, i < jump_tiles, i++)
					step(owner, owner.dir)
					if(i < jump_tiles / 2)
						owner.pixel_y += pixel_move
					else
						owner.pixel_y -= pixel_move
					sleep(sleep_time)

			usr.pixel_y = 0

			if (owner.bioHolder.HasEffect("fat") && prob(66) && !linked_power.safety)
				owner.visible_message("<span class='alert'><b>[owner]</b> crashes due to their heavy weight!</span>")
				playsound(usr.loc, "sound/impact_sounds/Wood_Hit_1.ogg", 50, 1)
				owner.changeStatus("weakened", 10 SECONDS)
				owner.changeStatus("stunned", 50)

			owner.layer = prevLayer

		if (istype(owner.loc,/obj/))
			var/obj/container = owner.loc
			boutput(owner, "<span class='alert'>You leap and slam your head against the inside of [container]! Ouch!</span>")
			owner.changeStatus("paralysis", 50)
			owner.changeStatus("weakened", 5 SECONDS)
			container.visible_message("<span class='alert'><b>[owner.loc]</b> emits a loud thump and rattles a bit.</span>")
			playsound(owner.loc, "sound/impact_sounds/Metal_Hit_Heavy_1.ogg", 50, 1)
			SPAWN_DBG(0)
				var/wiggle = 6
				while(wiggle > 0)
					wiggle--
					container.pixel_x = rand(-3,3)
					container.pixel_y = rand(-3,3)
					sleep(0.1 SECONDS)
				container.pixel_x = 0
				container.pixel_y = 0

		return

	cast_misfire()
		if (..())
			return 1

		if (ismob(owner.loc))
			boutput(usr, "<span class='alert'>You can't jump right now!</span>")
			return 1

		var/jump_tiles = 10
		var/pixel_move = 8
		var/sleep_time = 0.5
		if (linked_power.power)
			jump_tiles = 20
			pixel_move = 16
			sleep_time = 0.2

		if (istype(owner.loc,/turf/))
			usr.visible_message("<span class='alert'><b>[owner]</b> leaps far too high and comes crashing down hard!</span>")
			playsound(owner.loc, "sound/impact_sounds/Generic_Shove_1.ogg", 50, 1)
			playsound(owner.loc, "sound/impact_sounds/Wood_Hit_1.ogg", 50, 1)
			var/prevLayer = owner.layer
			owner.layer = EFFECTS_LAYER_BASE
			owner.changeStatus("weakened", 10 SECONDS)
			owner.changeStatus("stunned", 50)

			SPAWN_DBG(0)
				for(var/i=0, i < jump_tiles, i++)
					if(i < jump_tiles / 2)
						owner.pixel_y += pixel_move
					else
						owner.pixel_y -= pixel_move
					sleep(sleep_time)

			usr.pixel_y = 0

			owner.layer = prevLayer

		if (istype(owner.loc,/obj/))
			var/obj/container = owner.loc
			boutput(owner, "<span class='alert'>You leap and slam your head against the inside of [container]! Ouch!</span>")
			owner.changeStatus("paralysis", 50)
			owner.changeStatus("weakened", 5 SECONDS)
			container.visible_message("<span class='alert'><b>[owner.loc]</b> emits a loud thump and rattles a bit.</span>")
			playsound(owner.loc, "sound/impact_sounds/Metal_Hit_Heavy_1.ogg", 50, 1)
			SPAWN_DBG(0)
				var/wiggle = 6
				while(wiggle > 0)
					wiggle--
					container.pixel_x = rand(-3,3)
					container.pixel_y = rand(-3,3)
					sleep(0.1 SECONDS)
				container.pixel_x = 0
				container.pixel_y = 0

		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/polymorphism
	name = "Polymorphism"
	desc = "Enables the subject to reconfigure their appearance to mimic that of others."
	id = "polymorphism"
	msgGain = "You don't feel entirely like yourself somehow."
	msgLose = "You feel secure in your identity."
	cooldown = 1800
	probability = 66
	blockCount = 4
	blockGaps = 4
	stability_loss = 15
	ability_path = /datum/targetable/geneticsAbility/polymorphism

/datum/targetable/geneticsAbility/polymorphism
	name = "Polymorphism"
	desc = "Mimic the appearance of others."
	icon_state = "polymorphism"
	targeted = 1

	cast(atom/target)
		if (..())
			return 1

		if (!ishuman(target))
			boutput(owner, "<span class='alert'>[target] does not seem to be compatible with this ability.</span>")
			return 1
		if (target == owner)
			boutput(owner, "<span class='alert'>While \"be yourself\" is pretty good advice, that would be taking it a bit too literally.</span>")
			return 1
		var/mob/living/carbon/human/H = target
		if (!H.bioHolder)
			boutput(owner, "<span class='alert'>[target] does not seem to be compatible with this ability.</span>")
			return 1

		if (get_dist(H,owner) > 1 && !owner.bioHolder.HasEffect("telekinesis"))
			boutput(owner, "<span class='alert'>You must be within touching distance of [target] for this to work.</span>")
			return 1

		playsound(owner.loc, "sound/impact_sounds/Slimy_Hit_4.ogg", 50, 1)
		owner.visible_message("<span class='alert'><b>[owner]</b> touches [target], then begins to shifts and contort!</span>")

		SPAWN_DBG(1 SECOND)
			if(H && owner)
				playsound(owner.loc, "sound/impact_sounds/Flesh_Break_2.ogg", 50, 1)
				owner.bioHolder.CopyOther(H.bioHolder, copyAppearance = 1, copyPool = 0, copyEffectBlocks = 0, copyActiveEffects = 0)
				owner.real_name = H.real_name
				owner.name = H.name

		return

	cast_misfire(atom/target)
		if (..())
			return 1

		if (!ishuman(target))
			boutput(owner, "<span class='alert'>[target] does not seem to be compatible with this ability.</span>")
			return 1
		if (target == owner)
			boutput(owner, "<span class='alert'>While \"be yourself\" is pretty good advice, that would be taking it a bit too literally.</span>")
			return 1
		var/mob/living/carbon/human/H = target
		if (!H.bioHolder)
			boutput(owner, "<span class='alert'>[target] does not seem to be compatible with this ability.</span>")
			return 1

		if (get_dist(H,owner) > 1 && !owner.bioHolder.HasEffect("telekinesis"))
			boutput(owner, "<span class='alert'>You must be within touching distance of [target] for this to work.</span>")
			return 1

		playsound(owner.loc, "sound/impact_sounds/Slimy_Hit_4.ogg", 50, 1)
		owner.visible_message("<span class='alert'><b>[owner]</b> touches [target]... and nothing happens. Huh.</span>")

		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/telepathy
	name = "Telepathy"
	desc = "Allows the subject to project their thoughts into the minds of other organics."
	id = "telepathy"
	msgGain = "You can hear your own voice echoing in your mind."
	msgLose = "Your mental voice fades away."
	probability = 99
	blockCount = 4
	blockGaps = 2
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/telepathy

/datum/targetable/geneticsAbility/telepathy
	name = "Telepathy"
	desc = "Transmit psychic messages to others."
	icon_state = "telepathy"
	targeted = 1

	cast(atom/target)
		if (..())
			return 1

		var/mob/living/carbon/recipient = null
		if (iscarbon(target))
			recipient = target
		else if (ismob(target) && !iscarbon(target))
			boutput(owner, "<span class='alert'>You can't transmit to [target] as they are too different from you mentally!</span>")
			return 1
		else
			var/turf/T = get_turf(target)
			for (var/mob/living/carbon/C in T.contents)
				recipient = C
				break

		if (!recipient)
			boutput(owner, "<span class='alert'>There's nobody there to transmit a message to.</span>")
			return 1

		if (recipient.bioHolder.HasEffect("psy_resist"))
			boutput(owner, "<span class='alert'>You can't contact [recipient.name]'s mind at all!</span>")
			return 1

		if(!recipient.client || recipient.stat)
			boutput(recipient, "<span class='alert'>You can't seem to get through to [recipient.name] mentally.</span>")
			return 1

		var/msg = copytext( adminscrub(input(usr, "Message to [recipient.name]:","Telepathy") as text), 1, MAX_MESSAGE_LEN)
		if (!msg)
			return 1

		var/psyname = "A psychic voice"
		if (recipient.bioHolder.HasOneOfTheseEffects("telepathy","empath"))
			psyname = "[owner.name]"

		boutput(recipient, "<span style='color: #BD33D9'><b>[psyname]</b> echoes, \"<i>[msg]</i>\"</span>")
		boutput(owner, "<span style='color: #BD33D9'>You echo \"<i>[msg]</i>\" to <b>[recipient.name]</b>.</span>")

		logTheThing("telepathy", owner, recipient, "TELEPATHY to %target%: [msg]")

		return

	cast_misfire(atom/target)
		if (..())
			return 1

		var/mob/living/carbon/recipient = null
		if (iscarbon(target))
			recipient = target
		else if (ismob(target) && !iscarbon(target))
			boutput(owner, "<span class='alert'>You can't transmit to [target] as they are too different from you mentally!</span>")
			return 1
		else
			var/turf/T = get_turf(target)
			for (var/mob/living/carbon/C in T.contents)
				recipient = C
				break

		if (!recipient)
			boutput(owner, "<span class='alert'>There's nobody there to transmit a message to.</span>")
			return 1

		if (recipient.bioHolder.HasEffect("psy_resist"))
			boutput(owner, "<span class='alert'>You can't contact [recipient.name]'s mind at all!</span>")
			return 1

		if(!recipient.client || recipient.stat)
			boutput(recipient, "<span class='alert'>You can't seem to get through to [recipient.name] mentally.</span>")
			return 1

		var/msg = copytext( adminscrub(input(usr, "Message to [recipient.name]:","Telepathy") as text), 1, MAX_MESSAGE_LEN)
		if (!msg)
			return 1
		msg = uppertext(msg)

		owner.visible_message("<span class='alert'><b>[owner]</b> puts their fingers to their temples and stares at [target] really hard.</span>")
		owner.say(msg)

		logTheThing("telepathy", owner, recipient, "TELEPATHY misfire to %target%: [msg]")

		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/empath
	name = "Empathic Thought"
	desc = "The subject becomes able to read the minds of others for certain information."
	id = "empath"
	msgGain = "You suddenly notice more about others than you did before."
	msgLose = "You no longer feel able to sense intentions."
	probability = 99
	blockCount = 3
	blockGaps = 2
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/empath

/datum/targetable/geneticsAbility/empath
	name = "Mind Reader"
	desc = "Read the minds of others for information."
	icon_state = "empath"
	targeted = 1

	cast(atom/target)
		if (..())
			return 1

		var/mob/living/carbon/read = null
		if (iscarbon(target))
			read = target
		else if (ismob(read) && !iscarbon(target))
			boutput(owner, "<span class='alert'>You can't read the thoughts of [target] as they are too different from you mentally!</span>")
			return 1
		else
			var/turf/T = get_turf(target)
			for (var/mob/living/carbon/C in T.contents)
				read = C
				break

		if (!read)
			boutput(owner, "<span class='alert'>There's nobody there to read the thoughts of.</span>")
			return 1

		if (read.bioHolder.HasEffect("psy_resist"))
			boutput(owner, "<span class='alert'>You can't see into [read.name]'s mind at all!</span>")
			return 1

		if (isdead(read))
			boutput(owner, "<span class='alert'>[read.name] is dead and cannot have their mind read.</span>")
			return
		if (read.health < 0)
			boutput(owner, "<span class='alert'>[read.name] is dying, and their thoughts are too scrambled to read.</span>")
			return

		boutput(usr, "<span class='notice'>Mind Reading of [read.name]:</b></span>")
		var/pain_condition = read.health
		// lower health means more pain
		var/list/randomthoughts = list("what to have for lunch","the future","the past","money",
		"their hair","what to do next","their job","space","amusing things","sad things",
		"annoying things","happy things","something incoherent","something they did wrong")
		var/thoughts = "thinking about [pick(randomthoughts)]"
		if (read.getStatusDuration("burning"))
			pain_condition -= 50
			thoughts = "preoccupied with the fire"
		if (read.getStatusDuration("radiation"))
			pain_condition -= 25

		switch(pain_condition)
			if (81 to INFINITY)
				boutput(owner, "<span class='notice'><b>Condition</b>: [read.name] feels good.</span>")
			if (61 to 80)
				boutput(owner, "<span class='notice'><b>Condition</b>: [read.name] is suffering mild pain.</span>")
			if (41 to 60)
				boutput(owner, "<span class='notice'><b>Condition</b>: [read.name] is suffering significant pain.</span>")
			if (21 to 40)
				boutput(owner, "<span class='notice'><b>Condition</b>: [read.name] is suffering severe pain.</span>")
			else
				boutput(owner, "<span class='notice'><b>Condition</b>: [read.name] is suffering excruciating pain.</span>")
				thoughts = "haunted by their own mortality"

		switch(read.a_intent)
			if (INTENT_HELP)
				boutput(owner, "<span class='notice'><b>Mood</b>: You sense benevolent thoughts from [read.name].</span>")
			if (INTENT_DISARM)
				boutput(owner, "<span class='notice'><b>Mood</b>: You sense cautious thoughts from [read.name].</span>")
			if (INTENT_GRAB)
				boutput(owner, "<span class='notice'><b>Mood</b>: You sense hostile thoughts from [read.name].</span>")
			if (INTENT_HARM)
				boutput(owner, "<span class='notice'><b>Mood</b>: You sense cruel thoughts from [read.name].</span>")
				for(var/mob/living/L in view(7,read))
					if (L == read)
						continue
					thoughts = "thinking about punching [L.name]"
					break
			else
				boutput(owner, "<span class='notice'><b>Mood</b>: You sense strange thoughts from [read.name].</span>")

		if (ishuman(target))
			var/mob/living/carbon/human/H = read
			if (H.pin)
				boutput(owner, "<span class='notice'><b>Numbers</b>: You sense the number [H.pin] is important to [H.name].</span>")
		boutput(owner, "<span class='notice'><b>Thoughts</b>: [read.name] is currently [thoughts].</span>")

		if (read.bioHolder.HasEffect("empath"))
			boutput(read, "<span class='alert'>You sense [owner.name] reading your mind.</span>")
		else if (read.traitHolder.hasTrait("training_chaplain"))
			boutput(read, "<span class='alert'>You sense someone intruding upon your thoughts...</span>")
		return

	cast_misfire(atom/target)
		if (..())
			return 1

		var/mob/living/carbon/read = null
		if (iscarbon(target))
			read = target
		else if (ismob(read) && !iscarbon(target))
			boutput(owner, "<span class='alert'>You can't read the thoughts of [target] as they are too different from you mentally!</span>")
			return 1
		else
			var/turf/T = get_turf(target)
			for (var/mob/living/carbon/C in T.contents)
				read = C
				break

		if (!read)
			boutput(owner, "<span class='alert'>There's nobody there to read the thoughts of.</span>")
			return 1

		if (read.bioHolder.HasEffect("psy_resist"))
			boutput(owner, "<span class='alert'>You can't see into [read.name]'s mind at all!</span>")
			return 1

		if (isdead(read))
			boutput(owner, "<span class='alert'>[read.name] is dead and cannot have their mind read.</span>")
			return
		if (read.health < 0)
			boutput(owner, "<span class='alert'>[read.name] is dying, and their thoughts are too scrambled to read.</span>")
			return

		boutput(read, "<span class='alert'>Somehow, you sense <b>[owner]</b> trying and failing to read your mind!</span>")
		boutput(owner, "<span class='alert'>You are mentally overwhelmed by a huge barrage of worthless data!</span>")
		owner.emote("scream")
		owner.changeStatus("paralysis", 50)
		owner.changeStatus("stunned", 7 SECONDS)
		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/immolate
	name = "Incendiary Mitochondria"
	desc = "The subject becomes able to convert excess cellular energy into thermal energy."
	id = "immolate"
	msgGain = "You suddenly feel rather hot."
	msgLose = "You no longer feel uncomfortably hot."
	cooldown = 600
	probability = 66
	blockCount = 3
	blockGaps = 2
	stability_loss = 10
	ability_path = /datum/targetable/geneticsAbility/immolate

/datum/targetable/geneticsAbility/immolate
	name = "Immolate"
	desc = "Wreath yourself in burning flames."
	icon_state = "immolate"
	targeted = 0

	cast()
		if (..())
			return 1

		playsound(owner.loc, "sound/effects/mag_fireballlaunch.ogg", 50, 0)

		if (!linked_power.power && owner.bioHolder && (owner.bioHolder.HasEffect("fire_resist") || owner.bioHolder.HasEffect("thermal_resist")))
			owner.show_message("<span class='alert'>Your body emits an odd burnt odor but you somehow cannot bring yourself to heat up. Huh.</span>")
			return

		if (linked_power.power)
			owner.visible_message("<span class='alert'><b>[owner.name]</b> erupts into a huge column of flames! Holy shit!</span>")
			fireflash_sm(get_turf(owner), 3, 7000, 2000)
		else
			owner.visible_message("<span class='alert'><b>[owner.name]</b> suddenly bursts into flames!</span>")
			owner.set_burning(100)
		return

	cast_misfire()
		if (..())
			return 1

		playsound(owner.loc, "sound/effects/bamf.ogg", 50, 0)
		owner.show_message("<span class='alert'>You accidentally expunge all heat from your body. Whoops!</span>")
		owner.bodytemperature = 0

		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/melt
	name = "Self Biomass Manipulation"
	desc = "The subject becomes able to transform the matter of their cells into a liquid state."
	id = "melt"
	msgGain = "You feel strange and jiggly."
	msgLose = "You feel more solid."
	cooldown = 1200
	probability = 66
	blockCount = 3
	blockGaps = 2
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/melt

/datum/targetable/geneticsAbility/melt
	name = "Dissolve"
	desc = "Transform yourself into a liquid state."
	icon_state = "melt"
	targeted = 0

	cast()
		if (..())
			return 1

		if (linked_power.safety)
			// checks if it can be used, displaying a message to the user if not
			if (spell_invisibility(owner, 1, 0, 0, 1) == 1)
				spell_invisibility(owner, 50)
				playsound(owner.loc, "sound/effects/mag_phase.ogg", 25, 1, -1)
		else
			// should have synchronized it!
			owner.visible_message("<span class='alert'><b>[owner.name]'s flesh melts right off! Holy shit!</b></span>")
			if (owner.gender == "female")
				playsound(owner.loc, "sound/voice/screams/female_scream.ogg", 50, 0)
			else
				playsound(owner.loc, "sound/voice/screams/male_scream.ogg", 50, 0)
			playsound(owner.loc, "sound/effects/bubbles.ogg", 50, 0)
			playsound(owner.loc, "sound/impact_sounds/Flesh_Tear_2.ogg", 50, 0)
			if (ishuman(owner))
				var/mob/living/carbon/human/H = owner
				H.skeletonize()
				var/bdna = null // For forensics (Convair880).
				var/btype = null
				if (H.bioHolder.Uid && H.bioHolder.bloodType)
					bdna = H.bioHolder.Uid
					btype = H.bioHolder.bloodType
				gibs(owner.loc, null, null, bdna, btype)
			else
				owner.gib()

	cast_misfire()
		if (..())
			return 1

		playsound(owner.loc, "sound/effects/mag_golem.ogg", 50, 0)
		owner.visible_message("<span class='alert'><b>[owner.name] solidifies into a statue! Doubt they're coming back from that...</b></span>")
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.become_statue()
		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/superfart
	name = "High-Pressure Intestines"
	desc = "Vastly increases the gas capacity of the subject's digestive tract."
	id = "superfart"
	msgGain = "You feel bloated and gassy."
	msgLose = "You no longer feel gassy. What a relief!"
	cooldown = 900
	probability = 33
	blockCount = 4
	blockGaps = 3
	stability_loss = 15
	ability_path = /datum/targetable/geneticsAbility/superfart
	var/farting = 0

/datum/targetable/geneticsAbility/superfart
	name = "Superfart"
	desc = "Unleash a gigantic fart!"
	icon_state = "superfart"
	targeted = 0

	cast()
		if (..())
			return 1

		if (!farting_allowed)
			boutput(owner, "<span class='alert'>Farting is disabled.</span>")
			return 1
		var/datum/bioEffect/power/superfart/SF = linked_power

		if (SF.farting)
			boutput(owner, "<span class='alert'>You're already farting! Be patient!</span>")
			return 1

		owner.visible_message("<span class='alert'><b>[owner.name]</b> hunches down and grits their teeth!</span>")
		SF.farting = 1
		var/stun_time = 3
		var/fart_range = 6
		var/gib_user = 0
		var/throw_speed = 15
		var/throw_repeat = 3
		var/sound_volume = 50
		var/sound_repeat = 1
		var/fart_string = " unleashes a [pick("tremendous","gigantic","colossal")] fart!"

		if(linked_power.power)
			stun_time = 6
			fart_range = 12
			throw_speed = 30
			throw_repeat = 6
			sound_volume = 100
			sound_repeat = 3
			if (!linked_power.safety)
				gib_user = 1
				fart_string = "'s body is torn apart like a wet paper bag by [his_or_her(owner)] unbelievably powerful farting!"
				owner.unlock_medal("Shit Fest", 1)

		sleep(3 SECONDS)
		if (can_act(owner))
			if(owner.reagents.has_reagent("anti_fart"))
				owner.visible_message("<span class='alert'><b>[owner.name]</b> swells up. That can't be good.</span>")
				boutput(owner, "<span class='alert'><b>Oh god.</b></span>")
				indigestion_gib()
				return 1

			owner.visible_message("<span class='alert'><b>[owner.name]</b>[fart_string]</span>")
			while (sound_repeat > 0)
				sound_repeat--
				playsound(owner.loc, "sound/voice/farts/superfart.ogg", sound_volume, 1)

			for(var/mob/living/V in range(get_turf(owner),fart_range))
				shake_camera(V,10,5)
				if (V == owner)
					continue
				boutput(V, "<span class='alert'>You are sent flying!</span>")

				V.changeStatus("weakened", stun_time * 10)
				// why the hell was this set to 12 christ
				while (throw_repeat > 0)
					throw_repeat--
					step_away(V,get_turf(owner),throw_speed)

			if(owner.bioHolder.HasEffect("toxic_farts"))
				var/turf/fart_turf = get_turf(src)
				fart_turf.fluid_react_single("toxic_fart",50,airborne = 1)

			SF.farting = 0
			if (linked_power.power)
				for (var/turf/T in range(owner,6))
					animate_shake(T,5,rand(3,8),rand(3,8))
			if (gib_user)
				owner.gib()

			// Superfarted on the bible? Off to hell.
			for (var/obj/item/storage/bible/B in owner.loc)
				owner.damn()
				break
		else
			boutput(owner, "<span class='alert'>You were interrupted and couldn't fart! Rude!</span>")
			SF.farting = 0
			return 1

		return

	proc/indigestion_gib()
		owner.emote("faint")
		owner.setStatus("weakened", 20 SECONDS)
		owner.make_jittery(50)
		sleep(1 SECOND)
		owner.emote("scream")
		playsound(owner.loc, "sound/impact_sounds/Flesh_Tear_1.ogg", 100, 1)
		owner.TakeDamage("chest", 25, 0, 0, DAMAGE_BLUNT)
		owner.make_jittery(250)
		sleep(1 SECOND)
		owner.emote("scream")
		playsound(owner.loc, "sound/impact_sounds/Flesh_Tear_2.ogg", 100, 1)
		owner.TakeDamage("chest", 25, 0, 0, DAMAGE_BLUNT)
		owner.make_jittery(500)

		var/scream_time = 2
		var/scream_decrement = 0.25

		while(scream_time > 0)
			playsound(owner.loc, pick("sound/impact_sounds/Flesh_Break_1.ogg","sound/impact_sounds/Flesh_Tear_1.ogg","sound/impact_sounds/Flesh_Tear_2.ogg"), 100, 1)
			owner.emote("scream")
			sleep(scream_time SECONDS)
			scream_time -= scream_decrement
		owner.buttgib()
		return


/datum/bioEffect/power/superfart/griff
	name = "Very-High-Pressure Intestines"
	desc = "Immensely increases the gas capacity of the subject's digestive tract to near infinite levels."
	id = "superfartgriff"
	msgGain = "You feel INCREDIBLY bloated and gassy."
	msgLose = "You no longer feel INCREDIBLY gassy. What a relief!"

	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0

	stability_loss = 0
	cooldown = 200

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/eyebeams
	name = "Optic Energizer"
	desc = "Imbues the subject's eyes with the potential to project concentrated thermal energy."
	id = "eyebeams"
	msgGain = "Your eyes ache and burn."
	msgLose = "Your eyes stop aching."
	cooldown = 80
	probability = 33
	blockCount = 3
	blockGaps = 5
	stability_loss = 10
	ability_path = /datum/targetable/geneticsAbility/eyebeams
	var/projectile_path = "/datum/projectile/laser/eyebeams"

/datum/targetable/geneticsAbility/eyebeams
	name = "Eyebeams"
	desc = "Shoot lasers from your eyes."
	icon_state = "eyebeams"
	targeted = 1
	target_anything = 1
	needs_hands = 0

	cast(atom/target)
		if (..())
			return 1

		var/turf/T = get_turf(target)

		var/datum/bioEffect/power/eyebeams/EB = linked_power
		var/projectile_path = ispath(EB.projectile_path) ? EB.projectile_path : text2path(EB.projectile_path)
		if(linked_power.power)
			projectile_path = /datum/projectile/laser
		if (!ispath(projectile_path))
			projectile_path = /datum/projectile/laser/eyebeams

		owner.visible_message("<span class='alert'><b>[owner.name]</b> shoots eye beams!</span>")
		var/datum/projectile/laser/eyebeams/PJ = new projectile_path
		shoot_projectile_ST(owner, PJ, T)

	cast_misfire(atom/target)
		if (..())
			return 1

		owner.visible_message("<span class='alert'><b>[owner.name]'s</b> eyeballs catch on fire briefly!</span>")
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.take_eye_damage(5)

/datum/projectile/laser/eyebeams
	name = "optic laser"
	icon_state = "eyebeam"
	power = 20
	cost = 20
	sname = "eye laser"
	dissipation_delay = 5
	shot_sound = 'sound/weapons/TaserOLD.ogg'
	color_red = 1
	color_green = 0
	color_blue = 1

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/adrenaline
	name = "Adrenaline Rush"
	desc = "Enables the user to voluntarily empty glands of stimulants. May be dangerous with repeated use."
	id = "adrenaline"
	probability = 66
	blockCount = 4
	blockGaps = 4
	cooldown = 600
	msgGain = "You feel hype!"
	msgLose = "You don't feel so pumped anymore."
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/adrenaline

/datum/targetable/geneticsAbility/adrenaline
	name = "Adrenaline Rush"
	desc = "Infuse your bloodstream with stimulants."
	icon_state = "adrenaline"
	targeted = 0
	can_act_check = 0

	cast()
		if (..())
			return 1
		var/multiplier = 1
		if(linked_power.power)
			multiplier = 2
		if (owner.reagents)
			boutput(owner, "<span class='notice'>You get pumped up!</span>")
			owner.emote("scream")
			owner.reagents.add_reagent("epinephrine",20 * multiplier)
			owner.reagents.add_reagent("salicylic_acid",20 * multiplier)
			if(linked_power.safety)
				owner.reagents.add_reagent("methamphetamine",max(0,20 - owner.reagents.get_reagent_amount("methamphetamine")))
				owner.reagents.add_reagent("energydrink",max(0,5 - owner.reagents.get_reagent_amount("energydrink")))
			else
				owner.reagents.add_reagent("methamphetamine",20 * multiplier)
				owner.reagents.add_reagent("energydrink",5 * multiplier)

	cast_misfire()
		if (..())
			return 1
		var/multiplier = 4
		if (owner.reagents)
			boutput(owner, "<span class='alert'>You get pumped up! ...maybe a bit too pumped up! You feel kinda sick...</span>")
			owner.emote("scream")
			owner.reagents.add_reagent("epinephrine",20 * multiplier)
			owner.reagents.add_reagent("salicylic_acid",20 * multiplier)
			if(linked_power.safety)
				owner.reagents.add_reagent("methamphetamine",max(0,20 - owner.reagents.get_reagent_amount("methamphetamine")))
				owner.reagents.add_reagent("energydrink",max(0,5 - owner.reagents.get_reagent_amount("energydrink")))
			else
				owner.reagents.add_reagent("methamphetamine",20 * multiplier)
				owner.reagents.add_reagent("energydrink",5 * multiplier)

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/midas
	name = "Midas Touch"
	desc = "Allows the subject to transmute materials at will."
	id = "midas"
	msgGain = "Your fingers sparkle and gleam."
	msgLose = "Your fingers return to normal."
	cooldown = 300
	probability = 99
	blockCount = 2
	blockGaps = 4
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/midas
	var/transmute_material = "gold"

/datum/targetable/geneticsAbility/midas
	name = "Midas Touch"
	desc = "Transmute an object to gold by touching it."
	icon_state = "midas"
	targeted = 0

	cast()
		if (..())
			return 1
		if(linked_power.using)
			return 1

		var/base_path = /obj/item/
		if (linked_power.power)
			base_path = /obj/

		var/list/items = get_filtered_atoms_in_touch_range(owner,base_path)
		if (!items.len)
			boutput(usr, "/red You can't find anything nearby to touch.")
			return 1

		linked_power.using = 1
		var/obj/the_item = input("Which item do you want to transmute?","Midas Touch") as null|obj in items
		if (!the_item)
			last_cast = 0
			linked_power.using = 0
			return 1

		if (!linked_power)
			owner.visible_message("[owner] touches [the_item].")
		else
			if (istype(linked_power,/datum/bioEffect/power/midas))
				var/datum/bioEffect/power/midas/linked = linked_power
				owner.visible_message("<span class='alert'>[owner] touches [the_item], turning it to [linked.transmute_material]!</span>")
				the_item.setMaterial(getMaterial(linked.transmute_material))
			else
				owner.visible_message("<span class='alert'>[owner] touches [the_item], turning it to gold!</span>")
				the_item.setMaterial(getMaterial("gold"))
		linked_power.using = 0
		return

	cast_misfire()
		if (..())
			return 1
		if(linked_power.using)
			return 1

		var/base_path = /obj/item/
		if (linked_power.power)
			base_path = /obj/

		var/list/items = get_filtered_atoms_in_touch_range(owner,base_path)
		if (!items.len)
			boutput(usr, "/red You can't find anything nearby to touch.")
			return 1

		linked_power.using = 1
		var/obj/the_item = input("Which item do you want to transmute?","Midas Touch") as null|obj in items
		if (!the_item)
			last_cast = 0
			linked_power.using = 0
			return 1

		if (!linked_power)
			owner.visible_message("[owner] touches [the_item].")
		else
			owner.visible_message("<span class='alert'>[owner] touches [the_item], turning it to flesh!</span>")
			the_item.setMaterial(getMaterial("flesh"))
		linked_power.using = 0
		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/// COMBINATION-ONLY EFFECTS BELOW

/datum/bioEffect/power/healing_touch
	name = "Healing Touch"
	desc = "Allows the subject to heal the wounds of others with a touch."
	id = "healing_touch"
	msgGain = "Your hands radiate a comforting aura."
	msgLose = "The aura around your hands dissipates."
	cooldown = 900
	occur_in_genepools = 0
	stability_loss = 10
	ability_path = /datum/targetable/geneticsAbility/healing_touch

/datum/targetable/geneticsAbility/healing_touch
	name = "Healing Touch"
	desc = "Soothe the wounds of others."
	icon_state = "healingtouch"
	targeted = 1

	cast(atom/target)
		if (..())
			return 1

		if (get_dist(target,owner) > 1 && !owner.bioHolder.HasEffect("telekinesis"))
			boutput(usr, "<span class='alert'>You need to be closer to do that.</span>")
			return 1

		if (!iscarbon(target))
			boutput(usr, "<span class='alert'>This power won't work on that!</span>")
			return 1

		if (target == owner)
			boutput(usr, "<span class='alert'>This power doesn't work when you touch yourself. Weirdo.</span>")
			return 1

		var/mob/living/carbon/C = target
		owner.visible_message("<span class='alert'><b>[owner] touches [C], enveloping them in a soft glow!</b></span>")
		boutput(C, "<span class='notice'>You feel your pain fading away.</span>")
		var/amount_to_heal = 25
		if (linked_power.power)
			amount_to_heal = 50
		C.HealDamage("All", amount_to_heal, amount_to_heal)
		C.take_toxin_damage(0 - amount_to_heal)
		C.take_oxygen_deprivation(0 - amount_to_heal)
		C.take_brain_damage(0 - amount_to_heal)
		return

	cast_misfire(atom/target)
		if (..())
			return 1

		if (get_dist(target,owner) > 1 && !owner.bioHolder.HasEffect("telekinesis"))
			boutput(usr, "<span class='alert'>You need to be closer to do that.</span>")
			return 1

		if (!iscarbon(target))
			boutput(usr, "<span class='alert'>This power won't work on that!</span>")
			return 1

		if (target == owner)
			boutput(usr, "<span class='alert'>This power doesn't work when you touch yourself. Weirdo.</span>")
			return 1

		var/mob/living/carbon/C = target
		owner.visible_message("<span class='alert'><b>[owner] touches [C], enveloping them in a bright glow!</b></span>")
		boutput(C, "<span class='notice'>Your pain fades away rapidly.</span>")
		boutput(owner, "<span class='alert'>You use too much life energy and hurt yourself!</span>")
		var/amount_to_heal = 25
		if (linked_power.power)
			amount_to_heal = 50
		C.HealDamage("All", amount_to_heal, amount_to_heal)
		owner.TakeDamage("All", amount_to_heal, amount_to_heal)
		C.take_toxin_damage(0 - amount_to_heal)
		owner.take_toxin_damage(amount_to_heal)
		C.take_oxygen_deprivation(0 - amount_to_heal)
		owner.take_oxygen_deprivation(amount_to_heal)
		C.take_brain_damage(0 - amount_to_heal)
		owner.take_brain_damage(amount_to_heal)
		return

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/dimension_shift
	// cant get the scary shit to work so tOt for now
	name = "Dimension Shift"
	desc = "Phase out and hide in another dimension."
	id = "dimension_shift"
	msgGain = "You can see a faint blue light."
	msgLose = "The blue light fades away."
	cooldown = 900
	occur_in_genepools = 0
	stability_loss = 20
	ability_path = /datum/targetable/geneticsAbility/dimension_shift
	var/active = 0
	var/processing = 0
	var/atom/last_loc = null
	acceptable_in_mutini = 0

	OnRemove()
		..()
		if(active)
			processing = 1
			var/obj/dummy/spell_invis/invis_object
			if (istype(owner.loc,/obj/dummy/spell_invis/))
				invis_object = owner.loc
			owner.set_loc(last_loc)
			if (invis_object)
				qdel(invis_object)
			last_loc = null

			owner.visible_message("<span class='alert'><b>[owner] appears in a burst of blue light!</b></span>")
			playsound(owner.loc, "sound/effects/ghost2.ogg", 50, 0)
			SPAWN_DBG(0.7 SECONDS)
				animate(owner, alpha = 255, time = 5, easing = LINEAR_EASING)
				animate(color = "#FFFFFF", time = 5, easing = LINEAR_EASING)
				active = 0
			processing = 0
		return

/datum/targetable/geneticsAbility/dimension_shift
	name = "Dimension Shift"
	desc = "Hide in another dimension to avoid hazards."
	icon_state = "dimensionshift"
	targeted = 0
	cooldown = 900
	has_misfire = 0

	cast()
		if (..())
			return 1

		if (!istype(linked_power,/datum/bioEffect/power/dimension_shift))
			return 1
		var/datum/bioEffect/power/dimension_shift/P = linked_power

		if (!istype(owner.loc,/turf/) && !istype(owner.loc,/obj/dummy/spell_invis/))
			boutput(owner, "<span class='alert'>That won't work here.</span>")
			return 1

		if (P.processing)
			return 1

		P.processing = 1

		if (!P.active)
			P.active = 1
			P.last_loc = owner.loc
			owner.visible_message("<span class='alert'><b>[owner] vanishes in a burst of blue light!</b></span>")
			playsound(owner.loc, "sound/effects/ghost2.ogg", 50, 0)
			animate(owner, color = "#0000FF", time = 5, easing = LINEAR_EASING)
			animate(alpha = 0, time = 5, easing = LINEAR_EASING)
			SPAWN_DBG(0.7 SECONDS)
				var/obj/dummy/spell_invis/invis_object = new /obj/dummy/spell_invis(get_turf(owner))
				invis_object.canmove = 0
				owner.set_loc(invis_object)
			P.processing = 0
			return 1
		else
			var/obj/dummy/spell_invis/invis_object
			if (istype(owner.loc,/obj/dummy/spell_invis/))
				invis_object = owner.loc
			owner.set_loc(P.last_loc)
			if (invis_object)
				qdel(invis_object)
			P.last_loc = null

			owner.visible_message("<span class='alert'><b>[owner] appears in a burst of blue light!</b></span>")
			playsound(owner.loc, "sound/effects/ghost2.ogg", 50, 0)
			SPAWN_DBG(0.7 SECONDS)
				animate(owner, alpha = 255, time = 5, easing = LINEAR_EASING)
				animate(color = "#FFFFFF", time = 5, easing = LINEAR_EASING)
				P.active = 0
			P.processing = 0

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/photokinesis
	name = "Photokinesis"
	desc = "Allows the subject to create a source of light."
	id = "photokinesis"
	msgGain = "Everything seems too dark!"
	msgLose = "It's too bright!"
	cooldown = 600
	occur_in_genepools = 0
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/photokinesis
	var/red = 0
	var/green = 0
	var/blue = 0

	New()
		..()
		red = rand(5,10) / 10
		green = rand(5,10) / 10
		blue = rand(5,10) / 10

/datum/targetable/geneticsAbility/photokinesis
	name = "Photokinesis"
	desc = "Create a strong source of light."
	icon_state = "photokinesis"
	targeted = 1
	has_misfire = 0

	cast(atom/target)
		if (..())
			return 1
		if (!istype(linked_power,/datum/bioEffect/power/photokinesis/))
			return 1
		var/datum/bioEffect/power/photokinesis/P = linked_power

		var/turf/T = get_turf(target)
		owner.visible_message("<span class='alert'><b>[owner]</b> raises [his_or_her(owner)] hands into the air!</span>")
		playsound(owner.loc, "sound/voice/heavenly.ogg", 50, 0)
		var/strength = 7
		var/time = 300
		if (linked_power.power)
			strength = 13
			time = 600
		new /obj/photokinesis_light(T,P.red,P.green,P.blue,strength,time)

/obj/photokinesis_light
	name = ""
	desc = ""
	density = 0
	anchored = 1
	mouse_opacity = 0
	icon = null
	icon_state = null
	var/datum/light/light

	New(var/loc,var/color_R,var/color_G,var/color_B,var/strength = 7,var/time = 300)
		..()
		light = new /datum/light/point
		light.set_brightness(strength / 7)
		light.set_color(color_R, color_G, color_B)
		light.attach(src)
		light.enable()

		if (isnum(time))
			SPAWN_DBG(time)
				qdel(src)

	disposing()
		..()

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/erebokinesis
	name = "Erebokinesis"
	desc = "Allows the subject to snuff out all light in an area."
	id = "erebokinesis"
	msgGain = "Everything seems too bright!"
	msgLose = "It's too dark!"
	cooldown = 600
	occur_in_genepools = 0
	stability_loss = 5
	ability_path = /datum/targetable/geneticsAbility/erebokinesis
	var/time = 0
	var/size = 0

	New()
		..()
		size = rand(4, 6)
		time = rand(100, 300)

/datum/targetable/geneticsAbility/erebokinesis
	name = "Erebokinesis"
	desc = "Create a field of darkness."
	icon_state = "erebokinesis"
	targeted = 1
	has_misfire = 0

	cast(atom/target)
		if (..())
			return 1
		if (!istype(linked_power,/datum/bioEffect/power/erebokinesis/))
			return 1
		var/datum/bioEffect/power/erebokinesis/P = linked_power
		var/field_size = P.size
		var/field_time = P.time
		if (P.power)
			field_size *= 2
			field_time *= 2

		var/turf/T = get_turf(target)
		owner.visible_message("<span class='alert'><b>[owner]</b> raises [his_or_her(owner)] hands into the air!</span>")
		playsound(owner.loc, "sound/voice/chanting.ogg", 50, 0)
		new /obj/overlay/darkness_field(T, field_time, radius = 0.5 + field_size, max_alpha = 250)
		new /obj/overlay/darkness_field{plane = PLANE_SELFILLUM}(T, field_time, radius = 0.5 + field_size, max_alpha = 250)

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/fire_breath
	name = "Fire Breath"
	desc = "Allows the subject to exhale fire."
	id = "fire_breath"
	msgGain = "Your throat is burning!"
	msgLose = "Your throat feels a lot better now."
	cooldown = 600
	occur_in_genepools = 0
	stability_loss = 10
	ability_path = /datum/targetable/geneticsAbility/fire_breath
	var/temperature = 1200
	var/range = 4

/datum/targetable/geneticsAbility/fire_breath
	name = "Fire Breath"
	desc = "Huff and puff, and burn their house down!"
	icon_state = "firebreath"
	targeted = 1

	cast(atom/target)
		if (..())
			return 1

		var/turf/T = get_turf(target)
		var/list/affected_turfs = getline(owner, T)
		var/datum/bioEffect/power/fire_breath/FB = linked_power
		var/range = FB.range
		var/temp = FB.temperature
		if (FB.power)
			range *= 2
			temp *= 4
		owner.visible_message("<span class='alert'><b>[owner] breathes fire!</b></span>")
		playsound(owner.loc, "sound/effects/mag_fireballlaunch.ogg", 50, 0)
		var/turf/currentturf
		var/turf/previousturf
		for(var/turf/F in affected_turfs)
			previousturf = currentturf
			currentturf = F
			if(currentturf.density || istype(currentturf, /turf/space))
				break
			if(previousturf && LinkBlocked(previousturf, currentturf))
				break
			if (F == get_turf(owner))
				continue
			if (get_dist(owner,F) > range)
				continue
			tfireflash(F,0.5,temp)

	cast_misfire(atom/target)
		if (..())
			return 1

		owner.visible_message("<span class='alert'><b>[owner] manages to set themselves on fire!</b></span>")
		playsound(owner.loc, "sound/effects/mag_fireballlaunch.ogg", 50, 0)
		owner.set_burning(100)

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/brown_note
	name = "Brown Note"
	desc = "Allows the subject to emit noises that can cause involuntary flatus in others."
	id = "brown_note"
	msgGain = "You feel mischievous!"
	msgLose = "You want to behave yourself again."
	cooldown = 150
	blockCount = 1
	blockGaps = 3
	stability_loss = 15
	ability_path = /datum/targetable/geneticsAbility/brown_note

/datum/targetable/geneticsAbility/brown_note
	name = "Brown Note"
	desc = "Mess with others using the power of sound!"
	icon_state = "brownnote"
	targeted = 0

	cast()
		if (..())
			return 1
		owner.visible_message("<span class='alert'><b>[owner.name] makes a weird noise!</b></span>")
		playsound(owner.loc, 'sound/musical_instruments/WeirdHorn_0.ogg', 50, 0)
		for (var/mob/living/L in range(7,owner))
			if (L.hearing_check(1))
				if(locate(/obj/item/storage/bible) in get_turf(L))
					owner.visible_message("<span class='alert'><b>A mysterious force smites [owner.name] for inciting blasphemy!</b></span>")
					owner.gib()
				else
					L.emote("fart")

	cast_misfire()
		if (..())
			return 1
		owner.visible_message("<span class='alert'><b>[owner.name] makes a really weird noise!</b></span>")
		playsound(owner.loc, pick(soundCache), 50, 0)


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/telekinesis_drag
	name = "Telekinetic Pull"
	desc = "Allows the subject to influence physical objects through utilizing latent powers in their mind."
	id = "telekinesis_drag"
	effectType = effectTypePower
	probability = 8
	blockCount = 5
	blockGaps = 5
	reclaim_mats = 40
	msgGain = "You feel your consciousness expand outwards."
	msgLose = "Your conciousness closes inwards."
	stability_loss = 15
	ability_path = /datum/targetable/geneticsAbility/telekinesis

	OnMobDraw()
		if (disposed)
			return
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "telekinesishead[H.bioHolder.HasEffect("fat") ? "fat" :""]", layer = MOB_LAYER)
		return

	OnAdd()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.set_body_icon_dirty()

	OnRemove()
		..()
		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			H.set_body_icon_dirty()

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/telekinesis
	name = "Telekinesis"
	desc = "Allows the subject to influence physical objects through utilizing latent powers in their mind."
	id = "telekinesis_command"
	effectType = effectTypePower
	probability = 8
	blockCount = 5
	blockGaps = 5
	reclaim_mats = 40
	msgGain = "You feel your consciousness expand outwards."
	msgLose = "Your conciousness closes inwards."
	stability_loss = 30
	occur_in_genepools = 0
	ability_path = /datum/targetable/geneticsAbility/telekinesis

/datum/targetable/geneticsAbility/telekinesis
	name = "Telekinetic Throw"
	icon_state = "tk"
	desc = "Command a few objects to hurl themselves at the target location."
	targeted = 1
	target_anything = 1
	cooldown = 200

	cast(atom/T)
		var/list/thrown = list()
		var/current_prob = 100
		var/modifier = 0.4

		if (linked_power.power)
			modifier *= 2

		owner.visible_message("<span class='alert'><b>[owner.name]</b> makes a gesture at [T.name]!</span>")

		for (var/obj/O in view(7, owner))
			if (!O.anchored && isturf(O.loc))
				if (prob(current_prob))
					current_prob *= modifier // very steep. probably grabs 3 or 4 objects per cast -- much less effective than revenant command
					thrown += O
					animate_float(O)
		SPAWN_DBG(1 SECOND)
			for (var/obj/O in thrown)
				O.throw_at(T, 32, 2)

		return 0

	cast_misfire(atom/T)
		var/list/thrown = list()
		var/current_prob = 100
		var/modifier = 0.4

		if (linked_power.power)
			modifier *= 2

		owner.visible_message("<span class='alert'><b>[owner.name]</b> makes a gesture at [T.name]!</span>")

		for (var/obj/O in view(7, owner))
			if (!O.anchored && isturf(O.loc))
				if (prob(current_prob))
					current_prob *= modifier // very steep. probably grabs 3 or 4 objects per cast -- much less effective than revenant command
					thrown += O
					animate_float(O)
		SPAWN_DBG(1 SECOND)
			for (var/obj/O in thrown)
				O.throw_at(owner, 32, 2)

		return 0

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/darkcloak
	name = "Cloak of Darkness"
	desc = "Enables the subject to bend low levels of light around themselves, creating a cloaking effect."
	id = "cloak_of_darkness"
	effectType = effectTypePower
	isBad = 0
	probability = 33
	blockGaps = 3
	blockCount = 3
	msgGain = "You begin to fade into the shadows."
	msgLose = "You become fully visible."
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 3
	lockedChars = list("G","C","A","T")
	lockedTries = 8
	stability_loss = 20
	cooldown = 0
	var/active = 0
	ability_path = /datum/targetable/geneticsAbility/darkcloak

	proc/cloak_decloak(var/which_way = 1)
		if (!src.owner || !isliving(src.owner))
			return

		var/mob/living/L = owner
		if (which_way == 1)
			L.invisibility = 1
			L.UpdateOverlays(overlay_image, id)
		else
			L.invisibility = 0
			L.UpdateOverlays(null, id)

		return

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "aurapulse", layer = MOB_LIMB_LAYER)
			overlay_image.color = "#333333"
		..()
		owner.UpdateOverlays(null, id)

	OnRemove()
		..()
		src.cloak_decloak(2)
		return

	OnLife()
		if(..()) return
		if (isliving(owner))
			var/mob/living/L = owner
			var/turf/T = get_turf(L)

			if (T && isturf(T))
				var/area/A = get_area(T)
				if (istype(T, /turf/space) || (A && (istype(A, /area/shuttle/) || istype(A, /area/shuttle_transit_space) || A.name == "Space" || A.name == "Ocean")))
					src.cloak_decloak(2)

				else
					if (T.RL_GetBrightness() < 0.2 && can_act(owner) && src.active)
						src.cloak_decloak(1)
					else
						src.cloak_decloak(2)
			else
				src.cloak_decloak(2)
		return

/datum/targetable/geneticsAbility/darkcloak
	name = "Cloak of Darkness"
	icon_state = "darkcloak"
	desc = "Activate or deactivate your cloak of darkness."
	targeted = 0
	cooldown = 0
	can_act_check = 0
	has_misfire = 0

	cast(atom/T)
		var/datum/bioEffect/power/darkcloak/DC = linked_power
		if (DC.active)
			boutput(usr, "You stop using your cloak of darkness.")
			DC.active = 0
		else
			boutput(usr, "You start using your cloak of darkness.")
			DC.active = 1
		return 0

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/chameleon
	name = "Chameleon"
	desc = "The subject becomes able to subtly alter light patterns to become invisible, as long as they remain still."
	id = "chameleon"
	effectType = effectTypePower
	probability = 33
	blockCount = 3
	blockGaps = 3
	msgGain = "You feel one with your surroundings."
	msgLose = "You feel oddly exposed."
	lockProb = 40
	lockedGaps = 1
	lockedDiff = 3
	lockedChars = list("G","C","A","T")
	lockedTries = 8
	stability_loss = 20
	cooldown = 0
	var/active = 0
	ability_path = /datum/targetable/geneticsAbility/chameleon

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "aurapulse", layer = MOB_LIMB_LAYER)
		..()
		owner.UpdateOverlays(null, id)

	OnRemove()
		..()
		if (isliving(owner))
			var/mob/living/L = owner
			L.UpdateOverlays(null, id)
			L.invisibility = 0
		return

	OnLife()
		if(..()) return
		if(isliving(owner))
			var/mob/living/L = owner
			if ((world.timeofday - owner.l_move_time) >= 30 && can_act(owner) && src.active)
				L.UpdateOverlays(overlay_image, id)
				L.invisibility = 1
			else
				L.UpdateOverlays(null, id)
				L.invisibility = 0

/datum/targetable/geneticsAbility/chameleon
	name = "Chameleon"
	icon_state = "chameleon"
	desc = "Activate or deactivate your chameleon cloak."
	targeted = 0
	cooldown = 0
	can_act_check = 0
	has_misfire = 0

	cast(atom/T)
		var/datum/bioEffect/power/chameleon/CH = linked_power
		if (CH.active)
			boutput(usr, "You stop using your chameleon cloaking.")
			CH.active = 0
		else
			boutput(usr, "You start using your chameleon cloaking.")
			CH.active = 1
		return 0

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/bigpuke
	name = "Mass Emesis"
	desc = "Allows the subject to expel chemicals via the mouth."
	id = "bigpuke"
	msgGain = "You feel sick."
	msgLose = "You feel much better!"
	cooldown = 300
	occur_in_genepools = 0
	stability_loss = 10
	ability_path = /datum/targetable/geneticsAbility/bigpuke
	var/range = 3

/datum/targetable/geneticsAbility/bigpuke
	name = "Mass Emesis"
	desc = "BLAAAAAAAARFGHHHHHGHH"
	icon_state = "bigpuke"
	targeted = 1
	has_misfire = 0

	cast(atom/target)
		if (..())
			return 1

		var/turf/T = get_turf(target)
		var/list/affected_turfs = getline(owner, T)
		var/datum/bioEffect/power/bigpuke/BP = linked_power
		var/range = BP.range
		if (BP.power)
			range *= 2
		owner.visible_message("<span class='alert'><b>[owner] horfs up a huge stream of puke!</b></span>")
		logTheThing("combat", owner, target, "power-pukes [log_reagents(owner)] at [log_loc(owner)].")
		playsound(owner.loc, "sound/misc/meat_plop.ogg", 50, 0)
		owner.reagents.add_reagent("vomit",20)
		var/turf/currentturf
		var/turf/previousturf
		for(var/turf/F in affected_turfs)
			previousturf = currentturf
			currentturf = F
			if(currentturf.density || istype(currentturf, /turf/space))
				break
			if(previousturf && LinkBlocked(previousturf, currentturf))
				break
			if (F == get_turf(owner))
				continue
			if (get_dist(owner,F) > range)
				continue
			owner.reagents.reaction(F,TOUCH)
			for(var/mob/living/L in F.contents)
				owner.reagents.reaction(L,TOUCH)
			for(var/obj/O in F.contents)
				owner.reagents.reaction(O,TOUCH)
		owner.reagents.clear_reagents()
		return 0

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////

/datum/bioEffect/power/ink
	name = "Ink Glands"
	desc = "Allows the subject to expel modified melanin."
	id = "inkglands"
	msgGain = "You feel artistic."
	msgLose = "You don't really feel artistic anymore."
	cooldown = 0
	occur_in_genepools = 0
	stability_loss = 2
	ability_path = /datum/targetable/geneticsAbility/ink
	var/color = "#888888"

	New()
		..()
		color = random_color()

/datum/targetable/geneticsAbility/ink
	name = "Ink Glands"
	desc = "Spray colorful ink onto an object."
	icon_state = "ink"
	targeted = 0
	has_misfire = 0

	cast()
		if (..())
			return 1

		var/base_path = /obj
		var/list/items = get_filtered_atoms_in_touch_range(owner,base_path)
		if (!items.len)
			boutput(usr, "/red You can't find anything nearby to spray ink on.")
			return 1

		var/obj/the_item = input("Which item do you want to color?","Ink Glands") as null|obj in items
		if (!the_item)
			last_cast = 0
			return 1

		var/datum/bioEffect/power/ink/I = linked_power
		if (!linked_power)
			owner.visible_message("[owner] spits on [the_item]. Gross.")
		else
			owner.visible_message("<span class='alert'>[owner] sprays ink onto [the_item]!</span>")
			the_item.color = I.color
		return 0

/datum/bioEffect/power/shoot_limb
	name = "Vestigal Ballistics"
	desc = "Allows the subject to expel one of their limbs with considerable force."
	id = "shoot_limb"
	msgGain = "You feel intense pressure in your hip and shoulder joints."
	msgLose = "You joints feel much better!"
	cooldown = 600
	occur_in_genepools = 1
	probability = 10

	isBad = 1
	stability_loss = -15
	ability_path = /datum/targetable/geneticsAbility/shoot_limb
	var/count = 0
	var/const/ticks_to_explode = 200
	var/datum/targetable/geneticsAbility/shoot_limb/AB = null

	OnLife()
		..()

		if (count < ticks_to_explode)
			count++
			return
		else
			count = 0

		if (!src.safety && prob(70))



			if (ability)
				//Do I really even need this? I'm just putting it there in case the random turf is null. Which should never happen.
				var/do_count = 0
				do
					var/turf/T = locate(owner.x + rand(-3/2,3+2), owner.y+rand(-3/2,3/2), 1)
					if (T)
						if (ability.cast(T))
							return //no limbs left, no text!!!
						boutput(owner, "<span class='alert'>The pressure in one of your joints built up too high! One of your limbs flew off!</span>")
						owner.changeStatus("weakened", 4 SECONDS)
						return
				while (do_count < 5)


/datum/targetable/geneticsAbility/shoot_limb
	name = "Vestigal Ballistics"
	desc = "OOOOWWWWWW!!!!!!!!"
	icon_state = "shoot_limb"
	targeted = 1
	var/range = 9
	var/throw_power = 1
	var/limb_force = 20

	cast(atom/target)
		if (..())
			return 1

		if (ishuman(owner))
			var/mob/living/carbon/human/H = owner
			var/obj/item/parts/thrown_limb = null

			if (H.has_limb("l_arm"))
				thrown_limb = H.limbs.l_arm.remove(0)
			else if (H.has_limb("r_leg"))
				thrown_limb = H.limbs.r_leg.remove(0)
			else if (H.has_limb("l_leg"))
				thrown_limb = H.limbs.l_leg.remove(0)
			else if (H.has_limb("r_arm"))
				thrown_limb = H.limbs.r_arm.remove(0)
			else
				return 1
			SPAWN_DBG(1 DECI SECOND)
				if (istype(thrown_limb))
					//double power if the ability is empowered (doesn't really do anything, but w/e)
					var/tmp_force = thrown_limb.throwforce
					thrown_limb.throwforce = limb_force* (throw_power+1)	//double damage if empowered
					thrown_limb.throw_at(target, range, throw_power * (linked_power.power+1))
					//without snychronizer, you take damage and bleed on usage of the power
					if (!linked_power.safety)
						new thrown_limb.streak_decal(owner.loc)
						var/damage = rand(5,15)
						random_brute_damage(H, damage)
						take_bleeding_damage(H, damage)
						if(prob(60)) owner.emote("scream")

						//reset the time until the ability spontaniously fires
						var/datum/bioEffect/power/shoot_limb/pwr = linked_power
						if (istype(pwr))
							pwr.count = 0

					owner.visible_message("<span class='alert'><b>[thrown_limb][linked_power.power ? " violently " : " "]bursts off of its socket and flies towards [target]!</b></span>")
					logTheThing("combat", owner, target, "shoot_limb [!linked_power.safety ? "Accidently" : ""] at [ismob(target)].")
					SPAWN_DBG(1 SECOND)
						if (thrown_limb)
							thrown_limb.throwforce = tmp_force

////////////////
// Admin Only //
////////////////

/datum/bioEffect/power/fade_out
	name = "Fading"
	desc = "Allows the subject to become visible or invisible at will."
	id = "fade"
	cooldown = 0
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	ability_path = /datum/targetable/geneticsAbility/fade

/datum/targetable/geneticsAbility/fade
	name = "Fade"
	desc = "Fade in and out. An admin power."
	icon_state = "template"
	targeted = 0
	can_act_check = 0
	has_misfire = 0
	var/active = 0
	var/fading = 0

	cast()
		if (..())
			return 1

		if (fading)
			boutput(usr, "/red Already fading. Please wait a bit.")

		if (active)
			fading = 1
			animate(owner, time = 10, alpha = 0, easing = LINEAR_EASING)
			fading = 0
		else
			fading = 1
			animate(owner, time = 10, alpha = 255, easing = LINEAR_EASING)
			fading = 0

/datum/bioEffect/speech
	name = "Frontal Gyrus Alteration Type-N"
	desc = "Hinders nerve transmission to and from the speech center of the brain, resulting in faltering speech."
	id = "stutter"
	probability = 40
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "Y-you f.. feel a.. a bit n-n-nervous."
	msgLose = "You don't feel nervous anymore."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3
	icon_state = "speech"

	proc/OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = stutter(message)
		return message

/datum/bioEffect/speech/smile
	name = "Frontal Gyrus Alteration Type-S"
	desc = "Causes the speech center of the subject's brain to produce large amounts of seratonin when engaged."
	id = "accent_smiling"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel like you want to smile and smile and smile forever :)"
	msgLose = "You don't feel like smiling anymore. :("
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = smilify(message)
		return message

/datum/bioEffect/speech/elvis
	name = "Frontal Gyrus Alteration Type-E"
	desc = "Forces the language center of the subject's brain to drawl out sentences in a funky manner."
	id = "accent_elvis"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel funky."
	msgLose = "You feel a little less conversation would be great."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = elvisfy(message)
		return message

/datum/bioEffect/speech/chav
	name = "Frontal Gyrus Alteration Type-C"
	desc = "Forces the language center of the subject's brain to construct sentences in a more rudimentary manner."
	id = "accent_chav"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "Ye feel like a reet prat like, innit?"
	msgLose = "You no longer feel like being rude and sassy."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = chavify(message)
		return message

/datum/bioEffect/speech/scots
	name = "Frontal Gyrus Alteration Type-F"
	desc = "Forces the language center of the subject's brain to construct sentences in the manner of a highlander."
	id = "accent_scots"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "Och aye! You feel like a true Scot!"
	msgLose = "You weren't a true Scot after all."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = scotify(message)
		return message

/datum/bioEffect/speech/swedish
	name = "Frontal Gyrus Alteration Type-B"
	desc = "Forces the language center of the subject's brain to construct sentences in a vaguely norse manner."
	id = "accent_swedish"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel Swedish, however that works."
	msgLose = "The feeling of Swedishness passes."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = borkborkbork(message)
		return message

/datum/bioEffect/speech/finnish
	name = "Frontal Gyrus Alteration Type-FI"
	desc = "Forces the language center of the subject's brain to construct sentences in a manner not conclusively proven to exist by scientists."
	id = "accent_finnish"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "Sauna and birch beatings! Hyv!"
	msgLose = "The lure of the sauna subsides..."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = finnishify(message)
		return message

/datum/bioEffect/speech/tommy // DO NOT MAKE THIS APPEAR IN GENEPOOLS OR INTO A TRAIT OR ANY OF THAT, PLEASE, THANK YOU IN ADVANCE - with love, haine
	name = "Frontal Gyrus Alteration Type-T"
	desc = "Forces the langua.... what!? What the fuck is this? What happened here!? Gods have mercy on our souls."
	id = "accent_tommy"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel torn apart!"
	msgLose = "You pull yourself together."
	reclaim_fail = 10
	probability = 0 // NO
	occur_in_genepools = 0 // NO ALSO
	can_make_injector = 0
	can_copy = 0

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = tommify(message)
		return message

/*
/datum/bioEffect/speech/wonk // DO NOT MAKE THIS APPEAR IN GENEPOOLS OR INTO A TRAIT OR ANY OF THAT, PLEASE, THANK YOU IN ADVANCE - with love, haine
	name = "Frontal Gyrus Alteration Type-W"
	desc = "unfunny"
	id = "accent_wonk"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "Fuckable owwwwwwwls!"
	msgLose = "More like honk."
	reclaim_fail = 10
	probability = 0 // NO
	occur_in_genepools = 0 // NO ALSO
	can_make_injector = 0
	can_copy = 0

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = wonkify(message)
		return message
*/ //Actually, let's not have this appear fucking anywhere at all! ~warc


/datum/bioEffect/speech/comic
	name = "Frontal Gyrus Alteration Type-CS"
	desc = "Causes the speech center of the subject's brain to become, uh. Well, <i>something</i> happens to it."
	id = "accent_comic"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "<font face='Comic Sans MS'>You feel great!!</font>"
	msgLose = "You feel okay."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("T","A")
	lockedTries = 3

	OnAdd()
		var/mob/living/L = owner
		if (istype(L))
			L.speechpopupstyle = "font-family: 'Comic Sans MS'; font-size: 8px;"

	OnRemove()
		var/mob/living/L = owner
		if (istype(L))
			L.speechpopupstyle = ""

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		return message
		// just let this one handle itself for now

/datum/bioEffect/speech/slurring
	name = "Frontal Gyrus Alteration Type-D"
	desc = "Causes the subject to have impaired control over their oral muscles, resulting in malformed speech."
	id = "slurring"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel like your tongue's made out of lead."
	msgLose = "You feel less tongue-tied."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("T","A")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = say_drunk(message)
		return message

/datum/bioEffect/speech/unintelligable
	name = "Frontal Gyrus Alteration Type-X"
	desc = "Heavily corrupts the part of the brain responsible for forming spoken sentences."
	id = "unintelligable"
	isBad = 1
	effectType = effectTypeDisability
	blockCount = 4
	blockGaps = 4
	msgGain = "You can't seem to form any coherent thoughts!"
	msgLose = "Your mind feels more clear."
	reclaim_fail = 10
	stability_loss = -10
	lockProb = 75
	lockedGaps = 4
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = say_superdrunk(message)
		return message

/datum/bioEffect/speech/vowelitis
	name = "Frontal Gyrus Alteration Type-O"
	desc = "Causes the language center of the brain to have difficulty processing vowels."
	id = "vowelitis"
	effectType = effectTypePower
	msgGain = "You feel a bit tongue-tied."
	msgLose = "You no longer feel tongue-tied."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3
	var/vowel_lower = "a"
	var/vowel_upper = "A"

	New()
		..()
		var/picker = rand(1,5)
		switch(picker)
			if(1)
				vowel_lower = "a"
				vowel_upper = "A"
			if(2)
				vowel_lower = "e"
				vowel_upper = "E"
			if(3)
				vowel_lower = "i"
				vowel_upper = "I"
			if(4)
				vowel_lower = "o"
				vowel_upper = "O"
			if(5)
				vowel_lower = "u"
				vowel_upper = "U"
			else
				vowel_lower = ""
				vowel_upper = ""

	OnSpeak(var/message)
		if (!istext(message))
			return ""

		message = replacetext(message, "a", vowel_lower)
		message = replacetext(message, "e", vowel_lower)
		message = replacetext(message, "i", vowel_lower)
		message = replacetext(message, "o", vowel_lower)
		message = replacetext(message, "u", vowel_lower)
		message = replacetext(message, "A", vowel_upper)
		message = replacetext(message, "E", vowel_upper)
		message = replacetext(message, "I", vowel_upper)
		message = replacetext(message, "O", vowel_upper)
		message = replacetext(message, "U", vowel_upper)

		return message

/datum/bioEffect/speech/loud_voice
	name = "High-Pressure Larynx"
	desc = "Vastly increases airflow speed and capacity through the subject's larynx."
	id = "loud_voice"
	effectType = effectTypePower
	msgGain = "YOU SUDDENLY FEEL LIKE SHOUTING A WHOLE LOT!!!"
	msgLose = "You no longer feel the need to raise your voice."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""

		message = replacetext(message, "!", "!!!")
		message = replacetext(message, ".", "!!!")
		message = replacetext(message, "?", "???")
		message = uppertext(message)
		message += "!!!"

		return message

/datum/bioEffect/speech/reversed_speech
	name = "Frontal Gyrus Alteration Type-R"
	desc = "Causes the language center of the brain to process speech in reverse."
	id = "reversed_speech"
	effectType = effectTypePower
	msgGain = ".sdrawkcab tib a leef uoY"
	msgLose = "You feel the right way around."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""

		message = reverse_text(message)

		return message

/datum/bioEffect/speech/quiet_voice
	name = "Constricted Larynx"
	desc = "Decreases airflow speed and capacity through the subject's larynx."
	id = "quiet_voice"
	effectType = effectTypePower
	msgGain = "...you feel like being quiet..."
	msgLose = "You no longer feel the need to keep your voice down."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""

		message = replacetext(message, "!", "...")
		message = replacetext(message, "?", "..?")
		message = lowertext(message)
		message += "..."

		return message

/datum/bioEffect/monkey_speak
	name = "Monkey Speak"
	desc = "Causes the subject to understand monkeys."
	id = "monkey_speak"
	probability = 0
	msgGain = "You feel one with the jungle!"
	msgLose = "You feel less primal."

/datum/bioEffect/speech/zalgo
	name = "Eldritch Speech"
	desc = "The subject's larynx is channeling a chaotic dimension of elder beings."
	id = "accent_zalgo"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "HE COMES"
	msgLose = "You feel sane again."
	probability = 0
	occur_in_genepools = 0 // Probably shouldn't look like this? http://f.666kb.com/i/d2iqlzm1qa2gk6dqs.png
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0

	New()
		src.msgGain = zalgoify(src.msgGain, rand(0,8), rand(0, 2), rand(0, 8))
		..()

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = zalgoify(message, rand(0,2), rand(0, 1), rand(0, 2))
		return message


/datum/bioEffect/speech/void
	name = "Void"
	desc = "The subject's speech appears to come from multiple, shunted locations."
	id = "accent_void"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "Ah, friend! It's so good to be able to speak again!"
	msgLose = "Your voice is only yours again."
	probability = 0
	occur_in_genepools = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0

	New()
		src.msgGain = voidSpeak(src.msgGain)
		..()

	OnAdd()
		var/mob/living/L = owner
		L.speech_void = 1

	OnRemove()
		var/mob/living/L = owner
		L.speech_void = 0



/datum/bioEffect/speech/yee // DO NOT MAKE THIS APPEAR IN GENEPOOLS OR INTO A TRAIT OR ANY OF THAT, PLEASE, THANK YOU IN ADVANCE - with love, haine
	name = "yee"
	desc = "yee"
	id = "accent_yee"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "yee"
	msgLose = "nee"
	probability = 0 // noo
	occur_in_genepools = 0 // not for human consumption
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = yee_text(message)
		return message

/proc/yee_text(var/string)
	var/modded = ""
	var/datum/text_roamer/T = new/datum/text_roamer(string)

	for(var/i = 0, i < length(string), i=i)
		var/datum/parse_result/P = yee_parse(T)
		modded += P.string
		i += P.chars_used
		T.curr_char_pos = T.curr_char_pos + P.chars_used
		T.update()

	return modded

/proc/yee_parse(var/datum/text_roamer/R)
	var/new_string = ""
	var/used = 0

	switch(R.curr_char)
		if ("A","B","C","D","E","F","G","H","I","J","K","L","M","N","O","P","Q","R","S","T","U","V","W","X","Z") // all but Y
			if (R.prev_char == " " || R.curr_char_pos == 1) // start of a word or start of the string
				new_string = "Y"
				used = 1
			else
				new_string = "E"
				used = 1
		if ("a","b","c","d","e","f","g","h","i","j","k","l","m","n","o","p","q","r","s","t","u","v","w","x","y","z") // all but e
			if (R.prev_char == " " || R.curr_char_pos == 1) // start of a word or start of the string
				new_string = "y"
				used = 1
			else
				new_string = "e"
				used = 1

	if (new_string == "")
		new_string = R.curr_char
		used = 1

	var/datum/parse_result/P = new/datum/parse_result
	P.string = new_string
	P.chars_used = used
	return P

/datum/bioEffect/speech/butt
	name = "Frontal Gyrus Alteration Type-BT"
	desc = "Causes the language center of the brain to be connected to the subject's butt."
	id = "accent_butt"
	msgGain = "Your breath smells like a fart."
	msgLose = "Your breath no longer smells like a fart."
	reclaim_fail = 10
	stability_loss = -10
	lockProb = 75
	lockedGaps = 4
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3
	OnSpeak(var/message)
		if (!istext(message))
			return ""

		var/list/speech_list = splittext(message, " ")
		if(!speech_list || !speech_list.len)
			return ""

		var/num_butts = rand(1,4)
		var/counter = 0
		while(num_butts)
			counter++
			num_butts--
			speech_list[rand(1,speech_list.len)] = "butt"
			if(counter >= (speech_list.len / 2) )
				num_butts = 0

		return jointext(speech_list, " ")


/datum/bioEffect/speech/owowhatsthis //God is Dead
	name = "Frontal Gyrus Alteration Type-W"
	desc = "Reconstructs the language center of the subject's brain to create less threatening speech patterns."
	id = "accent_owo"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feew weawwy good!"
	msgLose = "You feel really good!"
	probability = 0 // no
	occur_in_genepools = 0 // also no
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = owotalk(message)
		return message

/datum/bioEffect/speech/french
	name = "Frontal Gyrus Alteration Type-Q"
	desc = "Forces the language center of the subject's brain to construct rude and vaguely-canadian sentences."
	id = "accent_french"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel like your province is a nation, however that works."
	msgLose = "The feeling of Independence passes."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = tabarnak(message)
		return message

/datum/bioEffect/speech/tyke
	name = "Frontal Gyrus Alteration Type-Y"
	desc = "Forces the language center of the subject's brain to construct sentences in the manner of a northerner."
	id = "accent_tyke"
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel like you're on Ilkley Moor without a hat."
	msgLose = "You never want to hear a brass band again."
	reclaim_fail = 10
	lockProb = 25
	lockedGaps = 2
	lockedDiff = 2
	lockedChars = list("G","C")
	lockedTries = 3

	OnSpeak(var/message)
		if (!istext(message))
			return ""
		message = yorkify(message)
		return message

/datum/bioEffect/glowy
	name = "Glowy"
	desc = "Endows the subject with bioluminescent skin. Color and intensity may vary by subject."
	id = "glowy"
	probability = 99
	effectType = effectTypePower
	blockCount = 3
	blockGaps = 1
	msgGain = "Your skin begins to glow softly."
	msgLose = "Your glow fades away."
	var/datum/light/light

	New()
		..()
		light = new /datum/light/point
		light.set_color(rand(1,10) / 10, rand(1,10) / 10, rand(1,10) / 10)
		light.set_brightness(0.7)

	OnAdd()
		..()
		light.attach(owner)
		light.enable()

	OnRemove()
		..()
		light.disable()
		light.detach()

/datum/bioEffect/horns
	name = "Cranial Keratin Formation"
	desc = "Enables the growth of a compacted keratin formation on the subject's head."
	id = "horns"
	effectType = effectTypePower
	probability = 99
	msgGain = "A pair of horns erupt from your head."
	msgLose = "Your horns crumble away into nothing."

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "horns", layer = MOB_LAYER)
		..()

/datum/bioEffect/horns/evil //this is just for /proc/soulcheck
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	id = "demon_horns"

/datum/bioEffect/particles
	name = "Dermal Glitter"
	desc = "Causes the subject's skin to shine and gleam."
	id = "shiny"
	effectType = effectTypePower
	probability = 66
	msgGain = "Your skin looks all blinged out."
	msgLose = "Your skin fades to a more normal state."
	var/system_path = /datum/particleSystem/sparkles

	OnAdd()
		if (!particleMaster.CheckSystemExists(system_path, owner))
			particleMaster.SpawnSystem(new system_path(owner))

	OnRemove()
		if (!particleMaster.CheckSystemExists(system_path, owner))
			particleMaster.RemoveSystem(system_path, owner)

/datum/bioEffect/color_changer
	name = "Melanin Suppressor"
	desc = "Shuts down all melanin production in the subject's body."
	id = "albinism"
	effectType = effectTypePower
	probability = 99
	isBad = 1
	var/eye_color_to_use = "#FF0000"
	var/color_to_use = "#FFFFFF"
	var/skintone_to_use = "#FFFFFF"
	var/holder_eyes = null
	var/holder_hair = null
	var/holder_det1 = null
	var/holder_det2 = null
	var/holder_skin = null

	OnAdd()
		if (!ishuman(owner))
			return

		var/mob/living/carbon/human/H = owner
		if (!H.bioHolder)
			return
		var/datum/bioHolder/B = H.bioHolder
		if (!B.mobAppearance)
			return
		var/datum/appearanceHolder/AH = H.bioHolder.mobAppearance
		holder_eyes = AH.e_color
		holder_hair = AH.customization_first_color
		holder_det1 = AH.customization_second_color
		holder_det2 = AH.customization_third_color
		holder_skin = AH.s_tone
		AH.e_color = eye_color_to_use
		AH.s_tone = skintone_to_use
		AH.customization_first_color = color_to_use
		AH.customization_second_color = color_to_use
		AH.customization_third_color = color_to_use
		H.update_face()
		H.update_body()

	OnRemove()
		if (!ishuman(owner))
			return

		var/mob/living/carbon/human/H = owner
		if (!H.bioHolder)
			return
		var/datum/bioHolder/B = H.bioHolder
		if (!B.mobAppearance)
			return
		var/datum/appearanceHolder/AH = H.bioHolder.mobAppearance
		AH.e_color = holder_eyes
		AH.s_tone = holder_skin
		AH.customization_first_color = holder_hair
		AH.customization_second_color = holder_det1
		AH.customization_third_color = holder_det2
		H.update_face()
		H.update_body()

/datum/bioEffect/color_changer/black
	name = "Melanin Stimulator"
	desc = "Overstimulates the subject's melanin glands."
	id = "melanism"
	effectType = effectTypePower
	probability = 99
	isBad = 1
	eye_color_to_use = "#572E0B"
	color_to_use = "#000000"
	skintone_to_use = "#000000"

/datum/bioEffect/stinky
	name = "Apocrine Enhancement"
	desc = "Increases the amount of natural body substances produced from the subject's apocrine glands."
	id = "stinky"
	probability = 99
	effectType = effectTypeDisability
	isBad = 1
	msgGain = "You feel sweaty."
	msgLose = "You feel much more hygenic."
	var/personalized_stink = "Wow, it stinks in here!"

	New()
		..()
		src.personalized_stink = stinkString()
		if (prob(5))
			src.variant = 2

	OnLife()
		if(..()) return
		if (owner.reagents.has_reagent("menthol"))
			return
		else if (prob(10))
			for(var/mob/living/carbon/C in view(6,get_turf(owner)))
				if (C == owner)
					continue
				if (src.variant == 2)
					boutput(C, "<span class='alert'>[src.personalized_stink]</span>")
				else
					boutput(C, "<span class='alert'>[stinkString()]</span>")

/datum/bioEffect/drunk
	name = "Ethanol Production"
	desc = "Encourages growth of ethanol-producing symbiotic fungus in the subject's body."
	id = "drunk"
	isBad = 1
	msgGain = "You feel drunk!"
	msgLose = "You feel sober."
	probability = 99
	var/reagent_to_add = "ethanol"
	var/reagent_threshold = 80
	var/add_per_tick = 1

	OnLife()
		if(..()) return
		var/mob/living/L = owner
		if (isdead(L))
			return
		if (L.reagents && L.reagents.get_reagent_amount(reagent_to_add) < reagent_threshold)
			L.reagents.add_reagent(reagent_to_add,add_per_tick)

/datum/bioEffect/drunk/bee
	name = "Bee Production"
	desc = "Encourages growth of bees in the subject's body."
	id = "drunk_bee"
	isBad = 0
	msgGain = "Your stomach buzzes!"
	msgLose = "The buzzing in your stomach stops."
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	reagent_to_add = "bee"
	reagent_threshold = 40
	add_per_tick = 1.2

/datum/bioEffect/drunk/pentetic
	name = "Pentetic Acid Production"
	desc = "This mutation somehow causes the subject's body to manufacture a potent chellating agent. How exactly it functions is completely unknown."
	id = "drunk_pentetic"
	msgGain = "You feel detoxified."
	msgLose = "You feel toxic."
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	reagent_to_add = "penteticacid"
	reagent_threshold = 40
	add_per_tick = 4

/datum/bioEffect/drunk/random
	name = "Chemical Production Modification"
	desc = {"This mutation somehow irreversibly alters the subject's body to function as an organic chemical factory, mass producing large quantities of seemingly random chemicals. The mechanism for this modification is currently unknown to medical and genetic science."}
	id = "drunk_random"
	msgGain = "You begin to sense an odd chemical taste in your mouth."
	msgLose = "The chemical taste in your mouth fades."
	occur_in_genepools = 1 //this is going to be very goddamn rare and very fucking difficult to unlock.
	mob_exclusive = /mob/living/carbon/human/
	probability = 1
	blockCount = 5
	can_research = 0
	lockProb = 100
	blockGaps = 0
	lockedGaps = 10
	lockedDiff = 6
	lockedChars = list("G","C","A","T","U")
	lockedTries = 10
	curable_by_mutadone = 0
	can_scramble = 0
	can_reclaim = 0
	reagent_to_add = "honey"
	reagent_threshold = 500 //it never stops
	add_per_tick = 5 //even more difficult to remove without calomel or hunchback

	New()
		..()
		if (all_functional_reagent_ids.len > 1)
			reagent_to_add = pick(all_functional_reagent_ids - list("big_bang_precursor", "big_bang", "nitrotri_parent", "nitrotri_wet", "nitrotri_dry"))
		else
			reagent_to_add = "water"

/datum/bioEffect/drunk/random/unstable
	name = "Unstable Chemical Production Modification"
	desc = {"This mutation somehow irreversibly alters the subject's body to function as an organic chemical factory, mass producing large quantities of seemingly random chemicals. The mechanism for this modification is currently unknown to medical and genetic science."}
	id = "drunk_random_unstable"
	probability = 0.25
	var/change_prob = 25
	add_per_tick = 7

	OnLife()
		if (prob(src.change_prob) && all_functional_reagent_ids.len > 1)
			reagent_to_add = pick(all_functional_reagent_ids)
		..()

/datum/bioEffect/bee
	name = "Apidae Metabolism"
	desc = {"Human worker clone batch #92 may contain inactive space bee DNA.
	If you do not have the authorization level to know that SS13 is staffed with clones, please forget this entire message."}
	id = "bee"
	msgGain = "You feel buzzed"
	msgLose = "You lose your buzz."
	probability = 99

/datum/bioEffect/chime_snaps
	name = "Dactyl Crystallization"
	desc = "The subject's digits crystallize and, when struck together, emit a pleasant noise."
	id = "chime_snaps"
	effectType = effectTypePower
	probability = 99
	msgGain = "Your fingers and toes turn transparent and crystalline."
	msgLose = "Your fingers and toes return to normal."

/datum/bioEffect/aura
	name = "Dermal Glow"
	desc = "Causes the subject's skin to emit faint light patterns."
	id = "aura"
	effectType = effectTypePower
	probability = 99
	msgGain = "You start to emit a pulsing glow."
	msgLose = "The glow in your skin fades."
	var/ovl_sprite = null
	var/color_hex = null

	New()
		..()
		ovl_sprite = pick("aurapulse","aurapulse-fast","aurapulse-slow","aurapulse-offset")
		color_hex = random_color()

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = ovl_sprite, layer = MOB_LIMB_LAYER)
			overlay_image.color = color_hex
		..()

/datum/bioEffect/chronal_additive_inversement
	name = "Chronal Additive Inversement"
	desc = "Causes the subject's age to become its additive inverse...somehow."
	id = "chronal_additive_inversement"
	effectType = effectTypePower
	probability = 66
	blockCount = 3
	blockGaps = 3
	msgGain = "You feel as if you're impossibly young."
	msgLose = "You feel like you're your own age again."
	stability_loss = 10

	OnAdd()
		holder.age = (0 - holder.age)
		..()
	OnRemove()
		holder.age = (0 - holder.age)
		..()

/datum/bioEffect/temporal_displacement
	name = "Temporal Displacement"
	desc = "The subject becomes displaced in time, aging them at random."
	id = "temporal_displacement"
	effectType = effectTypePower
	probability = 66
	blockCount = 3
	blockGaps = 3
	msgGain = "You feel like you're growing younger - no wait, older?"
	msgLose = "You feel like you're aging normally again."
	stability_loss = 10
	OnLife()
		..()
		if (prob(33))
			holder.age = rand(-80, 80)


//////////////////////
// Combination Only //
//////////////////////

/datum/bioEffect/fire_aura
	name = "Blazing Aura"
	desc = "Causes the subject's skin to emit harmless false fire."
	id = "aura_fire"
	effectType = effectTypePower
	occur_in_genepools = 0
	msgGain = "You burst into flames!"
	msgLose = "Your skin stops emitting fire."
	var/ovl_sprite = null
	var/color_hex = null

	New()
		..()
		color_hex = random_color()

	OnAdd()
		if (ishuman(owner))
			overlay_image = image("icon" = 'icons/effects/genetics.dmi', "icon_state" = "fireaura", layer = MOB_LIMB_LAYER)
			overlay_image.color = color_hex
		..()

/datum/bioEffect/fire_aura/evil //this is just for /proc/soulcheck
	occur_in_genepools = 0
	probability = 0
	scanner_visibility = 0
	can_research = 0
	can_make_injector = 0
	can_copy = 0
	can_reclaim = 0
	can_scramble = 0
	curable_by_mutadone = 0
	id = "hell_fire"`
        </script>
        <script>
            const regex = /\/datum\/geneticsrecipe\/(\w*).*\n.*list(.*)\n.*\/(.*)/g;
            const splitRegex = /(\w*)/g;

            var recipes = [];
            while ((m = regex.exec(recipeCombinationsRaw)) !== null)
            {
                if (m.index === regex.lastIndex)
                    regex.lastIndex++;
                
                recipes.push({
                    recipeName: m[1],
                    recipe: m[2].match(splitRegex).filter(n => n),
                    result: m[3]
                });
            }

            function recipeContains(recipe, filter)
            {
                return recipe.result.includes(filter) || recipe.recipeName.includes(filter) || recipe.recipe.toString().includes(filter);
            }

            function filter()
            {
                let table = document.getElementById("recipes");
                let filter = document.getElementById("filter").value;
                // Kill all children
                table.innerHTML = "";
                for (let r of recipes)
                {
                    if (filter != "" && !recipeContains(r, filter))
                        continue;

                    let row = document.createElement("tr");
                    let arr = [r.result, r.recipe.join(","), r.recipeName]
                    for (let a of arr)
                    {
                        let cell = document.createElement("td");
                        cell.innerText = a;
                        row.append(cell);
                    }
                    table.appendChild(row)
                }
            }
            //asap the site loads, put 
            window.onload = () =>
            {
                filter();
            }


        </script>
        <style>
            body
            {
                font-family: Sans-serif;
            }
            .main
            {
                margin: auto;
                width: 80%;
                padding-top: 5%;
            }

            .filter
            {
                width: 100%;
                height: 40px;
                padding: 10px;
                border-radius: 4px;
                border-width: 1px;
                border-color: #374350;
                font-size: larger;
            }

            .recipes
            {
                width: 100%;
            }
        </style>
    </head>
    <body>
        <div class="main">
            <input id="filter" class="filter" type="text" oninput="filter()" placeholder="Filter here">
            <div class="recipes">
                <table id="recipes" class="recipes">

                </table>
            </div>
        </div>
    </body>
</html>